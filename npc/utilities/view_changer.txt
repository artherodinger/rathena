-	script	viewChanger	-1,{
	if(!head1Bought || !head2Bought || !head3Bought || !head4Bought)
	mes "*จ่ายเมนูละรอบ \nจากนั้นจะสามารถใช้เมนูนั้น ได้ตลอดไปต่อตัวละคร";
	else
	mes "วัยรุ่นทำไรอะ?";
	mes " ";
	mes "*เลขหมวกที่ปลอดภัย (เช็คให้แล้วว่าไม่เด้ง) คือ 1-603";
	next;
	if(playerType == 1){
		.@head1String$ = head1Bought ? "" : " (" + callfunc("F_InsertComma",(.priceHead1 / 10000)) + " Bomberland Point)";
		.@head2String$ = head2Bought ? "" : " (" + callfunc("F_InsertComma",(.priceHead2 / 10000)) + " Bomberland Point)";
		.@head3String$ = head3Bought ? "" : " (" + callfunc("F_InsertComma",(.priceHead3 / 10000)) + " Bomberland Point)";
		.@head4String$ = head4Bought ? "" : " (" + callfunc("F_InsertComma",(.priceHead4 / 10000)) + " Bomberland Point)";
	}
	else{
		.@head1String$ = head1Bought ? "" : " (" + callfunc("F_InsertComma",.priceHead1) + "z)";
		.@head2String$ = head2Bought ? "" : " (" + callfunc("F_InsertComma",.priceHead2) + "z)";
		.@head3String$ = head3Bought ? "" : " (" + callfunc("F_InsertComma",.priceHead3) + "z)";
		.@head4String$ = head4Bought ? "" : " (" + callfunc("F_InsertComma",.priceHead4) + "z)";
	}
	
	menu "หมวก 1" + .@head1String$,L_Head1
	,"หมวก 2" + .@head2String$,L_Head2
	,"หมวก 3" + .@head3String$,L_Head3
	,"ผ้าคลุม" + .@head4String$,L_Head4;
	end;
	
	L_Head1:
	.@mode = 1;
	goto L_Trade;
	
	L_Head2:
	.@mode = 2;
	goto L_Trade;
	
	L_Head3:
	.@mode = 3;
	goto L_Trade;
	
	L_Head4:
	.@mode = 4;
	goto L_Trade;
	
	L_Trade:
	if(!getd("head" + .@mode + "Bought")){
		menu "จ่ายเงิน",-;
		if(playerType == 1){
			if(blPoint < (getd(".priceHead" + .@mode) / 10000))
			goto L_NotEnoughZeny;
			blPoint -= (getd(".priceHead" + .@mode) / 10000);
		}
		else{
			if(Zeny < getd(".priceHead" + .@mode))
			goto L_NotEnoughZeny;
			Zeny -= getd(".priceHead" + .@mode);
		}
		set getd("head" + .@mode + "Bought"),1;
	}
	if(.@mode == 4)
	mes "มีผ้าคลุมเลข " + .lowestHat4 + "-" + .highestHat4;
	else
	mes "มีหมวกเลข " + .lowestHat + "-" + .highestHat;
	next;
	menu "ดูไปเรื่อย ๆ",-,"ใส่เลขเอง",L_Input;
	.@i = getd("head" + .@mode);
	if(.i > 0)
	.@i = .i;
	.@isNoInput = 1;
	L_Input:
	if(!.@isNoInput)
	input .@i;
	L_Continue:
	if(.@isNoInput)
	.@i++;

	// Error handler
	for(.@allHeadNotAvailable = 0; .@allHeadNotAvailable < getarraysize($headNotAvailable); .@allHeadNotAvailable++){
		if(.@i == $headNotAvailable[.@allHeadNotAvailable])
		.@i++;
	}
	
	.aid = getcharid(3);
	.i = .@i;
	initnpctimer;
	startnpctimer;
	if(.@i >= .lowestHat && .@i <= .highestHat){
		if(.@mode == 1)
		changelook LOOK_HEAD_TOP,.@i;
		else if(.@mode == 2)
		changelook LOOK_HEAD_MID,.@i;
		else if(.@mode == 3)
		changelook LOOK_HEAD_BOTTOM,.@i;
		else if(.@mode == 4)
		changelook LOOK_ROBE,.@i;
	}
	else{
		.@i = 0;
		if(.@mode == 1)
		changelook LOOK_HEAD_TOP,.@i;
		else if(.@mode == 2)
		changelook LOOK_HEAD_MID,.@i;
		else if(.@mode == 3)
		changelook LOOK_HEAD_BOTTOM,.@i;
		else if(.@mode == 4)
		changelook LOOK_ROBE,.@i;
	}
	message strcharinfo(0),"สไตล์ที่: " + .@i;
	if(.@isNoInput)
	menu "ดูไปเรื่อย ๆ",L_Continue,"เอาอันนี้ละ",-;
	else
	menu "เอาอันนี้ละ",-;
	set getd("head" + .@mode),.@i;
	if(.@mode == 1)
	setlook LOOK_HEAD_TOP,.@i;
	else if(.@mode == 2)
	setlook LOOK_HEAD_MID,.@i;
	else if(.@mode == 3)
	setlook LOOK_HEAD_BOTTOM,.@i;
	else if(.@mode == 4)
	setlook LOOK_ROBE,.@i;
	message strcharinfo(0),"บันทึก เรียบร้อยแล้ว";
	end;

	L_NotEnoughZeny:
	if(playerType == 1)
	mes "ดูเหมือนเจ้าจะมี Bomberland Point ไม่เพียงพอ";
	else
	mes "ดูเหมือนเจ้าจะมี Zeny ไม่เพียงพอ";
	close;
	
OnTimer2000:
	if(!isloggedin(.aid)){
		//debugmes "Not logged in .i: " + .i;
		.@isFoundOldSave = 0;
		for(.@allHeadNotAvailable = 0; .@allHeadNotAvailable < getarraysize($headNotAvailable); .@allHeadNotAvailable++){
			if(.i == $headNotAvailable[.@allHeadNotAvailable])
			{
				.@isFoundOldSave = 1;
				break;
			}
		}
		if(!.@isFoundOldSave)
		setarray $headNotAvailable[getarraysize($headNotAvailable)],.i;
	}
	stopnpctimer;
	end;

OnInit:
	.priceHead1 = 10000;
	.priceHead2 = 500000;
	.priceHead3 = 1000000;
	.priceHead4 = 2000000;
	.lowestHat = 1;
	.highestHat = 2129;
	.lowestHat4 = 1;
	.highestHat4 = 110;
	if(getarraysize($headNotAvailable) <= 0)
	setarray $headNotAvailable[0],2021,2022,323,328,330,332,333,338,339,342,343,349,366,374,382,383,391,393,396,405,407,409,410,412,416,418,419,420,423,424,425,426,427,428,430,431,433,434,435,451,457,462,469,471,474,475,481,482,483,486,487,488,489,494,496,498,499,500,502,505,506,507,514,516,517,518,519,520,522,542,544,552,556,557,559,561,566,567,570,573,576,580,582,585,588,590,592,599,601,604,605;
	end;
}

-	script	backToTown	-1,{
	setpcblock PCBLOCK_ALL,false;
	if(playerType == 1)
	warp "moscovia",221,191;
	else
	warp "prontera",156,191;
	end;
	
OnPCLoadMapEvent:
	if(strcharinfo(3) == strnpcinfo(4)){
		setpcblock PCBLOCK_ALL,true;
		setpcblock PCBLOCK_MOVE,false;
		setpcblock PCBLOCK_CHAT,false;
		setpcblock PCBLOCK_SITSTAND,false;
		setpcblock PCBLOCK_NPCCLICK,false;
		setpcblock PCBLOCK_EMOTION,false;
	}
	end;
}

-	script	goToDress	-1,{
	setpcblock PCBLOCK_ALL,true;
	setpcblock PCBLOCK_MOVE,false;
	setpcblock PCBLOCK_CHAT,false;
	setpcblock PCBLOCK_SITSTAND,false;
	setpcblock PCBLOCK_NPCCLICK,false;
	setpcblock PCBLOCK_EMOTION,false;
	warp "prt_in",240,162;
	end;
	
OnInit:
	waitingroom "ใส่ หมวก, ปีก",0;
	end;
}

prontera,175,177,3	duplicate(goToDress)	ไปแต่งตัวจัดเต็ม	101
moscovia,210,194,5	duplicate(goToDress)	ไปแต่งตัวจัดเต็ม#2	101
prt_in,244,168,5	duplicate(viewChanger)	แต่งทุกอย่าง	1681
prt_in,235,163,5	duplicate(backToTown)	กลับเมือง	101
prt_in	mapflag	nodrop
prt_in	mapflag	loadevent
prt_in	mapflag	partylock

