-	script	deadSystem	-1,{
OnPCDieEvent:

	if(playerType == 1)
	lastHintMapName$ = "";

	// Killer name information
	if(killerrid > 0 && getunitname(killerrid) != ""){
		lastKiller$ = getunitname(killerrid);
		message strcharinfo(0),lastKiller$ + " ฆ่าคุณ! พิมพ์ @mskill เพื่อตรวจสอบ Skill ที่ Monster มี";
	}

	.@deadMapName$ = strcharinfo(3);
	if(
			// Check no death penalty maps
			.@deadMapName$ == "pvp_y_7-2"
			|| .@deadMapName$ == "pvp_y_8-2"
			|| .@deadMapName$ == "malaya"
			|| .@deadMapName$ == "que_moon"
			|| .@deadMapName$ == "prontera"
			|| .@deadMapName$ == "prt_in"
			// Check Bomberland players
			|| playerType == 1
			)
	end;
	
	// Mystery - Room Players
	if(playerType == 2 && strcharinfo(3) == "bat_c01"){
		BaseEXP = 0;
		JobExp = 0;
		BaseLevel -= 1;
		if(readparam(bStr) > 1)
		statusup2 bStr,-1;
		if(readparam(bAgi) > 1)
		statusup2 bAgi,-1;
		if(readparam(bVit) > 1)
		statusup2 bVit,-1;
		if(readparam(bInt) > 1)
		statusup2 bInt,-1;
		if(readparam(bDex) > 1)
		statusup2 bDex,-1;
		if(readparam(bLuk) > 1)
		statusup2 bLuk,-1;
		$mysteryDeadCount++;
		if($mysteryDeadCount < 5)
		mapannounce "bat_c01",strcharinfo(0) + " ตาย!! สามารถตายได้อีก " + (5 - $mysteryDeadCount) + " ครั้ง ก่อนที่ Monster จะหายไปและโดนลดระดับลง 1 ระดับ",0,0xFF2828;
		else{
			// Clean up monster
			killmonsterall strcharinfo(3);
			// Set monster dead
			$mysterySpawner = 0;
			// Reset dead count
			$mysteryDeadCount = 0;
			// Announce
			mapannounce "bat_c01",strcharinfo(0) + " ตาย!! Monster ได้หายไปแล้วและโดนลดชั้นลง 1 ชั้น",0;
			// Show monster spawner npc
			hideoffnpc "เริ่มต่อสู้";
			// Decrease mystery level
			$mysteryLevel -= 2;
			if($mysteryLevel <= 0)
			$mysteryLevel = 0;
		}
	}
	// Monster - Room Players
	else if(playerType <= 0){
		// Check Head-Top
		.@headTopId = getequipid(EQI_HEAD_TOP);
		if(.@headTopId > 0){
			.@equipmentName$ = getequipname(EQI_HEAD_TOP);
			// Check Refine Count
			.@refineCount = getequiprefinerycnt(EQI_HEAD_TOP);
			if(.@refineCount >= (BaseLevel / 10)){
				message strcharinfo(0),.@equipmentName$ + " ได้กันตายให้คุณ! จำนวนครั้งที่สามารถกันตายได้คงเหลือ 0 ครั้ง";
				specialeffect2 939;
				downrefitem EQI_HEAD_TOP,.@refineCount;
				end;
			}
			else if(.@refineCount > 0)
			message strcharinfo(0),"จำนวนตีบวก " + .@equipmentName$ + " ไม่เพียงพอ ต้องการ +" + (BaseLevel / 10);
		}

		// Set alive state to dead
		reviver = 0;
		
		// Set savepoint to afterlife world
		savepoint "job4_mag",130,194;
		
		// Dead announce
		announce strcharinfo(0) + " ตาย!",bc_all,0xFF2828;

		// Reset spawn count and kill count to invoke maps
		for(.@i = 1; .@i <= 20; .@i++){
			if(strcharinfo(3) == getd("$mr" + .@i + "name$")){
				set getd("$killed_mr" + .@i),0;
				set getd("$mr" + .@i),30;
				killmonsterall strcharinfo(3);
				break;
			}
		}
	}
	end;
}

-	script	monsterSkillCommand	-1,{
	end;
	
OnCheckMonsterSkill:
	mes "ตรวจสอบสกิลที่ Monster มีได้ที่ <URL>Link<INFO>https://raw.githubusercontent.com/kaninhot004/rathena/master/db/re/mob_skill_db.txt</INFO></URL>";
	mes "กด Ctrl + F แล้วพิมพ์ชื่อ " + lastKiller$;
	close;
	
OnInit:
	bindatcmd "mskill",strnpcinfo(3) + "::OnCheckMonsterSkill",0,99;
	end;
}
