-	script	bomberianWarper	-1,{
	if(strcharinfo(3) != "moscovia"){
		.@stageNumber = atoi(strnpcinfo(2));
		if(getd("blStageWarper" + .@stageNumber) <= 0){
			specialeffect2 569;
			set getd("blStageWarper" + .@stageNumber),1;
			message strcharinfo(0),"บันทึก Fast Travel Stage " + (.@stageNumber + 1) + " เรียบร้อยแล้ว!";
		}
		menu "กลับเมืองหลัก",-,"Fast Travel",L_FastTravel;
		warp "moscovia",221,191;
		end;
	}
	mes "Bomb Bomberian! เจ้าต้องการที่จะเล่นโหมดตลุยด่านหรือไม่?";
	mes "---";
	mes "*หากผ่านด่าน จะได้รับ \n(10 คูณ ด่าน) Bomberland Point";
	next;
	menu "วิธีผ่านด่าน",L_HowToPlay,"เริ่มตลุยด่าน",L_Stage1,"Fast Travel",L_FastTravel,"ขอ Trap เพิ่มหน่อย",-;
	mes "จัดไป";
	next;
	if (checkweight(1065,1000)){
		getitem 1065,1000;
		mes "เรียบร้อย";
		specialeffect2 269;
	}
	else
	mes "ถือ Trap เยอะไปรึเปล่า?";
	close;
	
	L_HowToPlay:
	mes "แนวทางที่เป็นไปได้ ในการผ่านด่าน มีดังนี้";
	mes "1.) ฆ่า Monster ให้ครบจำนวน";
	mes "2.) ฆ่า Boss ประจำ Map";
	mes "3.) เดินชนวงแหวนสีดำ (Unlocker)";
	mes "*การเดินทางไปด่านต่อไป ให้ดูจุดแดงที่ Minimap";
	close;
	
	L_Stage1:
	warp $blStageMapName$[0],$blStageMapX[0],$blStageMapY[0];
	callsub L_WarpBegin,strcharinfo(3);
	end;
	
	L_FastTravel:
	menu "Stage 1",L_FastTravelStage1
	,"Stage 2",L_FastTravelStage2
	,"Stage 3",L_FastTravelStage3
	,"Stage 4",L_FastTravelStage4
	,"Stage 5",L_FastTravelStage5
	,"Stage 6",L_FastTravelStage6
	,"Stage 7",L_FastTravelStage7
	,"Stage 8",L_FastTravelStage8
	,"Stage 9",L_FastTravelStage9
	;
	end;
	
	L_FastTravelStage1:
	.@targetStage = 0;
	goto L_FastTravelNow;
	L_FastTravelStage2:
	.@targetStage = 1;
	goto L_FastTravelNow;
	L_FastTravelStage3:
	.@targetStage = 2;
	goto L_FastTravelNow;
	L_FastTravelStage4:
	.@targetStage = 3;
	goto L_FastTravelNow;
	L_FastTravelStage5:
	.@targetStage = 4;
	goto L_FastTravelNow;
	L_FastTravelStage6:
	.@targetStage = 5;
	goto L_FastTravelNow;
	L_FastTravelStage7:
	.@targetStage = 6;
	goto L_FastTravelNow;
	L_FastTravelStage8:
	.@targetStage = 7;
	goto L_FastTravelNow;
	L_FastTravelStage9:
	.@targetStage = 8;
	goto L_FastTravelNow;
	
	L_FastTravelNow:
	if(getd("blStageWarper" + .@targetStage) <= 0){
		message strcharinfo(0),"คุณยังไปไม่ถึงด่านดังกล่าว";
		end;
	}
	atcommand "@tonpc Bomberian Warper#" + .@targetStage;
	callsub L_WarpBegin,strcharinfo(3);
	end;
	
	L_WarpBegin:
	lastWarpMapName$ = getarg(0);
	setpcblock PCBLOCK_ALL,true;
	setpcblock PCBLOCK_MOVE,false;
	addtimer 2000,strnpcinfo(3) + "::OnLoaded2Second";
	return;
	
OnLoaded2Second:
	if(strcharinfo(3) == lastWarpMapName$)
	setpcblock PCBLOCK_ALL,false;
	end;
	
OnInit:
	waitingroom "Bomberian ตลุยด่าน!",0;
	end;
}

-	script	bomberianUnlocker	-1,{
	end;
	
OnTouch:
	.@stageIndex = atoi(strnpcinfo(2));
	
	if($blStageCondition[.@stageIndex] != 3)
	end;

	if(getd("blKilled" + .@stageIndex) < 1){
		set getd("blKilled" + .@stageIndex),1;
		message strcharinfo(0),"คุณได้แตะ Unlocker เรียบร้อยแล้ว สามารถผ่านด่านได้เลย";
		specialeffect2 682;
		.@stageClearReward = (10 * (.@stageIndex + 1));
		blPoint += .@stageClearReward;
		dispbottom "คุณได้รับ " + callfunc("F_InsertComma",.@stageClearReward) + " Bomberland Point รวม " + callfunc("F_InsertComma",blPoint) + " Bomberland Point";
	}
	end;
}

-	script	bomberianStage	-1,{
	end;
	
OnBossDead:
	BaseExp = 0;
	JobExp = 0;

	.@stageIndex = atoi(strnpcinfo(2));

	if($blStageCondition[.@stageIndex] != 2)
	end;

	// Add party member
	.@partyId = getcharid(1);
	if(.@partyId > 0)
	addrid(2,0,.@partyId);

	// Kill target
	// Check killed monster id
	if(killedrid == $blStageBossId[.@stageIndex]){
		if(getd("blKilled" + .@stageIndex) < 1){
			set getd("blKilled" + .@stageIndex),1;
			message strcharinfo(0),"คุณฆ่าเป้าหมายเรียบร้อยแล้ว สามารถผ่านด่านได้เลย";
			specialeffect2 682;
			.@stageClearReward = (10 * (.@stageIndex + 1));
			blPoint += .@stageClearReward;
			dispbottom "คุณได้รับ " + callfunc("F_InsertComma",.@stageClearReward) + " Bomberland Point รวม " + callfunc("F_InsertComma",blPoint) + " Bomberland Point";
		}
	}
	
	end;
	
OnMonsterDead:
	BaseExp = 0;
	JobExp = 0;

	.@stageIndex = atoi(strnpcinfo(2));
	
	if($blStageCondition[.@stageIndex] != 1)
	end;

	// Add party member
	.@partyId = getcharid(1);
	if(.@partyId > 0)
	addrid(2,0,.@partyId);

	// Kill count
	// Check kill count
	if(getd("blKilled" + .@stageIndex) < .killCountNeed){
		set getd("blKilled" + .@stageIndex),getd("blKilled" + .@stageIndex) + 1;
		if(getd("blKilled" + .@stageIndex) >= .killCountNeed){
			set getd("blKilled" + .@stageIndex),.killCountNeed;
			specialeffect2 682;
			.@stageClearReward = (10 * (.@stageIndex + 1));
			blPoint += .@stageClearReward;
			dispbottom "คุณได้รับ " + callfunc("F_InsertComma",.@stageClearReward) + " Bomberland Point รวม " + callfunc("F_InsertComma",blPoint) + " Bomberland Point";
		}
		message strcharinfo(0),"กำจัดเป้าหมายแล้ว " + getd("blKilled" + .@stageIndex) + "/" + .killCountNeed;
	}

	end;

OnInit:
	// Unexpected error
	.@mapName$ = strnpcinfo(4);

	if(.@mapName$ == "")
	end;

	// Start OnInit
	
	.killCountNeed = 20;
	
	deletearray $blStage0Id[0],getarraysize($blStage0Id);
	deletearray $blStage1Id[0],getarraysize($blStage1Id);
	deletearray $blStage2Id[0],getarraysize($blStage2Id);
	deletearray $blStage3Id[0],getarraysize($blStage3Id);
	deletearray $blStage4Id[0],getarraysize($blStage4Id);
	deletearray $blStage5Id[0],getarraysize($blStage5Id);
	deletearray $blStage6Id[0],getarraysize($blStage6Id);
	deletearray $blStage7Id[0],getarraysize($blStage7Id);
	deletearray $blStage8Id[0],getarraysize($blStage8Id);
	setarray $blStage0Id[0],1002;
	setarray $blStage1Id[0],1002,1004;
	setarray $blStage2Id[0],1007,1009;
	setarray $blStage3Id[0],1001,1005,1010;
	setarray $blStage4Id[0],1005,1013;
	setarray $blStage5Id[0],1014,1018,1019;
	setarray $blStage6Id[0],1023;
	setarray $blStage7Id[0],1033,1035,1040;
	setarray $blStage8Id[0],1052,1056,1057;
	
	deletearray $blStageBossId[0],getarraysize($blStageBossId);
	setarray $blStageBossId[0],0
	,1031
	,0
	,0
	,0
	,0
	,1026
	,0
	,1065
	;

	deletearray $blStageMapName$[0],getarraysize($blStageMapName$);
	setarray $blStageMapName$[0],"mosk_fild02"
	,"mosk_dun01"
	,"mosk_dun02"
	,"mosk_dun03"
	,"mosk_que"
	,"hu_fild02"
	,"cave"
	,"odin_tem01"
	,"odin_tem02"
	;
	
	deletearray $blStageMapX[0],getarraysize($blStageMapX);
	setarray $blStageMapX[0],18
	,25
	,166
	,32
	,50
	,293
	,99
	,88
	,28
	;
	
	deletearray $blStageMapY[0],getarraysize($blStageMapY);
	setarray $blStageMapY[0],171
	,75
	,28
	,136
	,17
	,29
	,22
	,146
	,335
	;
	
	deletearray $blStageBossX[0],getarraysize($blStageBossX);
	setarray $blStageBossX[0],0
	,0
	,0
	,0
	,0
	,0
	,110
	,0
	,226
	;
	
	deletearray $blStageBossY[0],getarraysize($blStageBossY);
	setarray $blStageBossY[0],0
	,0
	,0
	,0
	,0
	,0
	,67
	,0
	,150
	;

	deletearray $blStageMobCount[0],getarraysize($blStageMobCount);
	setarray $blStageMobCount[0],50
	,75
	,100
	,100
	,50
	,125
	,25
	,150
	,100
	;
	
	deletearray $blStageCondition[0],getarraysize($blStageCondition);
	setarray $blStageCondition[0],1
	,2
	,3
	,1
	,1
	,3
	,2
	,1
	,2
	;
	
	//debugmes "Start spawn monster on map " + strnpcinfo(4);
	
	callsub L_RespawnMob;
	
	initnpctimer;
	end;

	L_RespawnMob:
	.@stageIndex = atoi(strnpcinfo(2));
	//debugmes gettimetick(1) + ": " + .@stageIndex;
	.@mobAmount = $blStageMobCount[.@stageIndex];
	//debugmes ".@mobAmount: " + .@mobAmount;
	.@mapName$ = strnpcinfo(4);
	.@currentMonsterCount = getmapunits(BL_MOB,.@mapName$);
	//debugmes ".@currentMonsterCount: " + .@currentMonsterCount;
	.@maxMonster = .@mobAmount - .@currentMonsterCount;
	//debugmes ".@maxMonster: " + .@maxMonster;
	if(.@maxMonster > 1)
	.@mobAmount = rand((.@maxMonster / 2),.@maxMonster);
	else if(.@maxMonster > 0)
	.@mobAmount = 1;
	else
	return;
	
	//debugmes ".@mobAmount (Calculated, Spawning now): " + .@mobAmount;
	
	.@arraySize = getarraysize(getd("$blStage" + .@stageIndex + "Id"));
	for(.@i = 0; .@i < .@arraySize; .@i++){
		monster .@mapName$,0,0,"--ja--",getd("$blStage" + .@stageIndex + "Id[" + .@i + "]"),(.@mobAmount / .@arraySize),strnpcinfo(0) + "::OnMonsterDead";
		for(.@j = 0; .@j < (.@mobAmount / .@arraySize); .@j++)
		setunitdata $@mobid[.@j],UMOB_MODE,(0x0000001 | 0x0000004 | 0x0000080);
	}
	.@bossId = $blStageBossId[.@stageIndex];
	if(.@bossId > 0){
		monster .@mapName$,$blStageBossX[.@stageIndex],$blStageBossY[.@stageIndex],"--ja--",.@bossId,1,strnpcinfo(0) + "::OnBossDead";
		setunitdata $@mobid[0],UMOB_MODE,(0x0000001 | 0x0000004 | 0x0000080);
	}
	return;

OnTimer60000:
	callsub L_RespawnMob;
	initnpctimer;
	end;

OnTouch:
	// Unexpected error
	.@mapName$ = strnpcinfo(4);

	if(.@mapName$ == "")
	end;

	// Start OnTouch
	
	.@stageIndex = atoi(strnpcinfo(2));
	
	if(($blStageCondition[.@stageIndex] == 1 && getd("blKilled" + .@stageIndex) >= .killCountNeed)
			|| ($blStageCondition[.@stageIndex] != 1 && getd("blKilled" + .@stageIndex) >= 1)){
		mes "ยินดีด้วยคุณผ่านด่านแล้ว!";
		menu "ไปด่านต่อไป",-,"กลับเมืองหลัก",L_ReturnToMainTown;
		.@stageIndex++;
		if(getarraysize($blStageMapName$) > .@stageIndex){
			warp $blStageMapName$[.@stageIndex],$blStageMapX[.@stageIndex],$blStageMapY[.@stageIndex];
			callsub L_WarpBegin,strcharinfo(3);
		}
		else
		message strcharinfo(0),"ยังไม่มีด่านต่อไป หากถูกใจแนวนี้ ช่วย Donate ให้ผมด้วยนะครับ";
		end;
	}
	else{
		if($blStageCondition[.@stageIndex] == 1)
		message strcharinfo(0),"ต้องกำจัดเป้าหมายให้ครบเสียก่อน (" + getd("blKilled" + .@stageIndex) + "/" + .killCountNeed + ")";
		else if($blStageCondition[.@stageIndex] == 2)
		message strcharinfo(0),"ต้องกำจัด " + getmonsterinfo(getd("$blStageBossId[" + .@stageIndex + "]"),MOB_NAME) + " ให้ได้เสียก่อน";
		else if($blStageCondition[.@stageIndex] == 3)
		message strcharinfo(0),"ต้องแตะ Unlocker ให้ได้เสียก่อน";
	}
	end;
	
	L_ReturnToMainTown:
	warp "moscovia",221,191;
	end;

	L_WarpBegin:
	lastWarpMapName$ = getarg(0);
	setpcblock PCBLOCK_ALL,true;
	setpcblock PCBLOCK_MOVE,false;
	addtimer 2000,strnpcinfo(3) + "::OnLoaded2Second";
	return;
	
OnLoaded2Second:
	if(strcharinfo(3) == lastWarpMapName$)
	setpcblock PCBLOCK_ALL,false;
	end;
}

-	script	stageHint	-1,{
	end;
	
OnTouch:
	// Hint player where to go
	getmapxy(.@mapName$,.@x,.@y,BL_NPC,"ชนเพื่อผ่านด่าน#" + strnpcinfo(2));
	viewpoint 1,.@x,.@y,1,0xFF0000;
	
	.@stageIndex = atoi(strnpcinfo(2));
	
	if($blStageCondition[.@stageIndex] == 1)
	message strcharinfo(0),"เป้าหมาย: กำจัด Monster ให้ครบ | สมบัติภายในด่าน: " + (getd("blHiddenTreasure" + .@stageIndex) ? "พบแล้ว" : "ยังไม่พบ");
	else if($blStageCondition[.@stageIndex] == 2)
	message strcharinfo(0),"เป้าหมาย: กำจัด " + getmonsterinfo(getd("$blStageBossId[" + .@stageIndex + "]"),MOB_NAME) + " | สมบัติภายในด่าน: " + (getd("blHiddenTreasure" + .@stageIndex) ? "พบแล้ว" : "ยังไม่พบ");
	else if($blStageCondition[.@stageIndex] == 3)
	message strcharinfo(0),"เป้าหมาย: ตามหา Unlocker | สมบัติภายในด่าน: " + (getd("blHiddenTreasure" + .@stageIndex) ? "พบแล้ว" : "ยังไม่พบ");
	
	end;
}

-	script	hiddenTreasure	-1,{
	end;
	
OnTimer10000:
OnTimer20000:
OnTimer30000:
OnTimer40000:
OnTimer50000:
OnTimer60000:
OnTimer70000:
OnTimer80000:
OnTimer90000:
OnTimer100000:
OnTimer110000:
OnTimer120000:
OnTimer130000:
OnTimer140000:
OnTimer150000:
OnTimer160000:
OnTimer170000:
	specialeffect 488;
	end;
	
OnTimer180000:
OnInit:
	.@npcMapName$ = strnpcinfo(4);
	
	if(.@npcMapName$ == "")
	end;
	
	getfreecell(.@npcMapName$,.@x,.@y);
	moveNPC strnpcinfo(0),.@x,.@y;
	initnpctimer;
	end;

OnTouch:
	.@stageIndex = atoi(strnpcinfo(2));
	
	if(!getd("blHiddenTreasure" + .@stageIndex))
	{
		set getd("blHiddenTreasure" + .@stageIndex),1;
		
		// Stage 1
		if(.@stageIndex == 0){
			setarray .@OptID[0],2;
			setarray .@OptVal[0],10;
			setarray .@OptParam[0],0;
			getitem3 2604,1,1,0,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
			setarray .@OptID[0],12;
			setarray .@OptVal[0],10;
			setarray .@OptParam[0],0;
			getitem3 2604,1,1,0,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
		}
		// Stage 2
		else if(.@stageIndex == 1){
			setarray .@OptID[0],244;
			setarray .@OptVal[0],200;
			setarray .@OptParam[0],0;
			getitem3 5383,1,1,0,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
			setarray .@OptID[0],246;
			setarray .@OptVal[0],200;
			setarray .@OptParam[0],0;
			getitem3 13038,1,1,0,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
		}
		// Stage 3
		else if(.@stageIndex == 2){
			setarray .@OptID[0],4;
			setarray .@OptVal[0],5;
			setarray .@OptParam[0],0;
			getitem3 22060,1,1,0,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
			setarray .@OptID[0],7;
			setarray .@OptVal[0],5;
			setarray .@OptParam[0],0;
			getitem3 2503,1,1,0,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
		}
		// Stage 4
		else if(.@stageIndex == 3){
			setarray .@OptID[0],1,2,4;
			setarray .@OptVal[0],5,5,1;
			setarray .@OptParam[0],0,0,0;
			getitem3 2330,1,1,0,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
			setarray .@OptID[0],7,6,0;
			setarray .@OptVal[0],5,5,0;
			setarray .@OptParam[0],0,0,0;
			getitem3 2296,1,1,0,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
		}
		// Stage 5
		else if(.@stageIndex == 4){
			setarray .@OptID[0],12;
			setarray .@OptVal[0],200;
			setarray .@OptParam[0],0;
			getitem3 2269,1,1,0,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
			setarray .@OptID[0],245;
			setarray .@OptVal[0],200;
			setarray .@OptParam[0],0;
			getitem3 2892,1,1,0,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
		}
		// Stage 6
		else if(.@stageIndex == 5){
			setarray .@OptID[0],6;
			setarray .@OptVal[0],20;
			setarray .@OptParam[0],0;
			getitem3 2918,1,1,0,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
			setarray .@OptID[0],5;
			setarray .@OptVal[0],20;
			setarray .@OptParam[0],0;
			getitem3 2920,1,1,0,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
		}
		// Stage 7
		else if(.@stageIndex == 6){
			setarray .@OptID[0],2,244,245,246;
			setarray .@OptVal[0],10,250,250,250;
			setarray .@OptParam[0],0,0,0,0;
			getitem3 13069,1,1,0,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
		}
		// Stage 8
		else if(.@stageIndex == 7){
			setarray .@OptID[0],1,2,11,12;
			setarray .@OptVal[0],250,250,250,250;
			setarray .@OptParam[0],0,0,0,0;
			getitem3 18600,1,1,0,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
		}
		// Stage 9
		else if(.@stageIndex == 8){
			setarray .@OptID[0],4,6,7;
			setarray .@OptVal[0],33,33,33;
			setarray .@OptParam[0],0,0,0;
			getitem3 18703,1,1,0,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
		}
		message strcharinfo(0),"คุณเปิดกล่องสมบัติแปลก ๆ และพบสมบัติภายในนั้น!";
	}
	else
	message strcharinfo(0),"คุณเปิดกล่องสมบัติแปลก ๆ ของด่านนี้ไปแล้ว";
	
	end;
}

-	script	replenisher	-1,{
	message strcharinfo(0),"ยืนใกล้ ๆ แล้วคุณจะรู้สึกสดชื่น!";
	end;
	
OnTimer10000:
OnTimer20000:
OnTimer30000:
OnTimer40000:
OnTimer50000:
	getmapxy(.@mapName$,.@x,.@y,BL_NPC,strnpcinfo(0));
	areapercentheal .@mapName$,(.@x - 5),(.@y + 5),(.@x + 5),(.@y - 5),50,50;
	specialeffect EF_SANCTUARY;
	for(.@i = 0; .@i < .itemDropCount; .@i++){
		getfreecell(.@mapName$,.@x,.@y);
		makeitem 514,1,.@mapName$,.@x,.@y;
	}
	end;

OnTimer60000:
OnInit:
	.@npcMapName$ = strnpcinfo(4);
	
	if(.@npcMapName$ == "")
	end;
	
	getfreecell(.@npcMapName$,.@x,.@y);
	moveNPC strnpcinfo(0),.@x,.@y;
	initnpctimer;
	
	.itemDropCount = 4;
	end;
}

moscovia,213,207,5	duplicate(bomberianWarper)	Bomberian Warper	727

mosk_fild02,18,178,5	duplicate(bomberianWarper)	Bomberian Warper#0	727
mosk_fild02,190,255,4	duplicate(bomberianStage)	ชนเพื่อผ่านด่าน#0	722,3,3
mosk_fild02,1,1,5	duplicate(stageHint)	Stage Hint#0	111,400,400
mosk_fild02,1,1,5	duplicate(hiddenTreasure)	สมบัติลับ#0	2452,2,2
mosk_fild02,1,1,5	duplicate(replenisher)	ต้นไม้วิเศษ#0	1878

mosk_dun01,28,81,3	duplicate(bomberianWarper)	Bomberian Warper#1	727
mosk_dun01,200,273,4	duplicate(bomberianStage)	ชนเพื่อผ่านด่าน#1	722,3,3
mosk_dun01,1,1,5	duplicate(stageHint)	Stage Hint#1	111,400,400
mosk_dun01,1,1,5	duplicate(hiddenTreasure)	สมบัติลับ#1	2452,2,2
mosk_dun01,1,1,5	duplicate(replenisher)	ต้นไม้วิเศษ#1	1878

mosk_dun02,164,35,3	duplicate(bomberianWarper)	Bomberian Warper#2	727
mosk_dun02,130,155,4	duplicate(bomberianStage)	ชนเพื่อผ่านด่าน#2	722,3,3
mosk_dun02,1,1,5	duplicate(stageHint)	Stage Hint#2	111,400,400
mosk_dun02,1,1,5	duplicate(hiddenTreasure)	สมบัติลับ#2	2452,2,2
mosk_dun02,1,1,5	duplicate(replenisher)	ต้นไม้วิเศษ#2	1878
mosk_dun02,234,213,4	duplicate(bomberianUnlocker)	Unlocker#2	723,3,3

mosk_dun03,31,140,5	duplicate(bomberianWarper)	Bomberian Warper#3	727
mosk_dun03,205,211,4	duplicate(bomberianStage)	ชนเพื่อผ่านด่าน#3	722,3,3
mosk_dun03,1,1,5	duplicate(stageHint)	Stage Hint#3	111,400,400
mosk_dun03,1,1,5	duplicate(hiddenTreasure)	สมบัติลับ#3	2452,2,2
mosk_dun03,1,1,5	duplicate(replenisher)	ต้นไม้วิเศษ#3	1878

mosk_que,57,19,3	duplicate(bomberianWarper)	Bomberian Warper#4	727
mosk_que,48,217,4	duplicate(bomberianStage)	ชนเพื่อผ่านด่าน#4	722,3,3
mosk_que,1,1,5	duplicate(stageHint)	Stage Hint#4	111,400,400
mosk_que,1,1,5	duplicate(hiddenTreasure)	สมบัติลับ#4	2452,2,2
mosk_que,1,1,5	duplicate(replenisher)	ต้นไม้วิเศษ#4	1878

hu_fild02,289,36,5	duplicate(bomberianWarper)	Bomberian Warper#5	727
hu_fild02,147,261,4	duplicate(bomberianStage)	ชนเพื่อผ่านด่าน#5	722,3,3
hu_fild02,1,1,5	duplicate(stageHint)	Stage Hint#5	111,400,400
hu_fild02,1,1,5	duplicate(hiddenTreasure)	สมบัติลับ#5	2452,2,2
hu_fild02,1,1,5	duplicate(replenisher)	ต้นไม้วิเศษ#5	1878
hu_fild02,267,273,4	duplicate(bomberianUnlocker)	Unlocker#5	723,3,3

cave,108,33,3	duplicate(bomberianWarper)	Bomberian Warper#6	727
cave,83,100,4	duplicate(bomberianStage)	ชนเพื่อผ่านด่าน#6	722,3,3
cave,1,1,5	duplicate(stageHint)	Stage Hint#6	111,400,400
cave,1,1,5	duplicate(hiddenTreasure)	สมบัติลับ#6	2452,2,2
cave,1,1,5	duplicate(replenisher)	ต้นไม้วิเศษ#6	1878

odin_tem01,97,148,3	duplicate(bomberianWarper)	Bomberian Warper#7	727
odin_tem01,384,333,4	duplicate(bomberianStage)	ชนเพื่อผ่านด่าน#7	722,3,3
odin_tem01,1,1,5	duplicate(stageHint)	Stage Hint#7	111,400,400
odin_tem01,1,1,5	duplicate(hiddenTreasure)	สมบัติลับ#7	2452,2,2
odin_tem01,1,1,5	duplicate(replenisher)	ต้นไม้วิเศษ#7	1878

odin_tem02,31,340,3	duplicate(bomberianWarper)	Bomberian Warper#8	727
odin_tem02,152,350,4	duplicate(bomberianStage)	ชนเพื่อผ่านด่าน#8	722,3,3
odin_tem02,1,1,5	duplicate(stageHint)	Stage Hint#8	111,400,400
odin_tem02,1,1,5	duplicate(hiddenTreasure)	สมบัติลับ#8	2452,2,2
odin_tem02,1,1,5	duplicate(replenisher)	ต้นไม้วิเศษ#8	1878
