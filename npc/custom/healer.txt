//===== rAthena Script =======================================
//= Healer
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Basic healer script.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Aligned coordinates with @go.
//============================================================

-	script	Healer	-1,{
	.@Delay = 1;

	if (@HD > gettimetick(2)){
		mes "Buff อยู่ขั้นที่ " + upgradeBuff;
		next;
		menu "ไม่ทำอะไร",-,"Upgrade Buff",L_UpgradeBuff;
		close;
	}
	
	specialeffect2 EF_HEAL2;
	
	if(gethominfo(0) > 0){
		addhomintimacy 100000;
		atcommand "@homhungry 100";
		homevolution;
		hommutate;
	}
	
	identifyall;
	repairall;
	
	if(upgradeBuff >= 1)
	sc_start SC_INCREASEAGI,600000,10;
	if(upgradeBuff >= 2)
	sc_start SC_BLESSING,600000,10;
	if(upgradeBuff >= 3)
	sc_start SC_ANGELUS,600000,10;
	if(upgradeBuff >= 4)
	sc_start SC_IMPOSITIO,600000,5;
	if(upgradeBuff >= 5)
	sc_start SC_SUFFRAGIUM,600000,3;
	if(upgradeBuff >= 6)
	sc_start SC_KYRIE,600000,10;
	if(upgradeBuff >= 7)
	sc_start SC_MAGNIFICAT,600000,5;

	percentheal 100,100;

	if (.@Delay)
	@HD = gettimetick(2) + .@Delay;
	end;
	
	L_UpgradeBuff:
	
	mes "Buff ที่คุณจะได้รับขณะนี้";
	if(upgradeBuff >= 1)
	mes "Increase AGI";
	if(upgradeBuff >= 2)
	mes "Blessing";
	if(upgradeBuff >= 3)
	mes "Angelus";
	if(upgradeBuff >= 4)
	mes "Impositio";
	if(upgradeBuff >= 5)
	mes "Suffragium";
	if(upgradeBuff >= 6)
	mes "Kyrie Eleison";
	if(upgradeBuff >= 7)
	mes "Magnificat";

	if(upgradeBuff >= .maxBuffUpgrade){
		next;
		mes "ตอนนี้ Upgrade ถึงขั้นสูงสุดแล้ว โปรดรออัปเดตภายหลัง";
		close;
	}
	.@sumPrice = .basePrice;
	if(upgradeBuff > 0){
		.@sumUpgradeCount = upgradeBuff;
		while(.@sumUpgradeCount > 0){
			.@sumUpgradeCount--;
			.@sumPrice = .@sumPrice * .priceMultiplier;
		}
	}
	if(upgradeBuff < 10 && .@sumPrice > .maxPriceBelow10)
	.@sumPrice = .maxPriceBelow10;
	else if(upgradeBuff < 20 && .@sumPrice > .maxPriceBelow20)
	.@sumPrice = .maxPriceBelow20;
	else if(upgradeBuff < 30 && .@sumPrice > .maxPriceBelow30)
	.@sumPrice = .maxPriceBelow30;
	next;
	menu "จ่าย " + callfunc("F_InsertComma",.@sumPrice) + "z",-;
	if(Zeny >= .@sumPrice){
		Zeny -= .@sumPrice;
		upgradeBuff++;
		mes "เรียบร้อยแล้ว";
		mes "Buff อยู่ขั้นที่ " + upgradeBuff;
		close;
	}
	else{
		mes "เจ้ามี Zeny ไม่เพียงพอ";
		mes "ต้องการ  " + callfunc("F_InsertComma",.@sumPrice) + "z";
		close;
	}
	
OnInit:
	.maxBuffUpgrade = 7;
	.basePrice = 50;
	.priceMultiplier = 10;
	.maxPriceBelow10 = 50000;
	.maxPriceBelow20 = 500000;
	.maxPriceBelow30 = 5000000;
	waitingroom "กดรัว ๆ เพื่อ Upgrade Buff",0;
	end;
}


// Duplicates
//============================================================
prontera,160,193,4	duplicate(Healer)	ฟื้นฟูพลังชีวิต#prt	677
