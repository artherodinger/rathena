prontera,145,217,4	script	นักสู้	10298,{
	if(BaseClass == Job_Taekwon){
		mes .npcName$;
		mes "ว่าไงนักสู้ตัวน้อย จะมาทำอะไรเหรอ?";
		next;
		menu "ใช้ระบบ " + .systemName$,L_Use,"เติม " + .resourceName$,-;
		mes .npcName$;
		mes .resourceName$ + " สามารถอยู่ได้ " + .normalTimer + " นาที";
		mes .resourceName$ + "(Cash) สามารถอยู่ได้ " + .cashTimer + " นาที";
		mes "- ระดับ 1 โอกาสสำเร็จ " + .lv1Chance + "% (Lv. < " + .lv1LevelRequire + ")";
		mes "- ระดับ 2 โอกาสสำเร็จ " + .lv2Chance + "% (Lv. < " + .lv2LevelRequire + ")";
		mes "- ระดับ 3 โอกาสสำเร็จ " + .lv3Chance + "%";
		mes "^8328FF- Cash โอกาสสำเร็จ 100%^000000";
		next;
		if(getd("$" + .variableName$ + "Buff") > 0){
			mes .npcName$;
			if(getd("$" + .variableName$ + "Buff") == 4)
			mes "ขณะนี้มีการใช้งาน \n" + .systemName$ + " (Cash) อยู่ \nระยะเวลาที่เหลือ " + getd("$" + .variableName$ + "BuffTimer") + " นาที";
			else
			mes "ขณะนี้มีการใช้งาน \n" + .systemName$ + " ระดับ " + getd("$" + .variableName$ + "Buff") + " อยู่ \nระยะเวลาที่เหลือ " + getd("$" + .variableName$ + "BuffTimer") + " นาที";
		}
		if(BaseLevel < .lv1LevelRequire)
		menu "ใช้ " + .systemName$ + " ระดับ 1 (ลด 10 Level)",L_Request1,"ใช้แบบ Cash",L_CashRequest;
		else if(BaseLevel < .lv2LevelRequire)
		menu "ใช้ " + .systemName$ + " ระดับ 2 (ลด 10 Level)",L_Request2,"ใช้แบบ Cash",L_CashRequest;
		else
		menu "ใช้ " + .systemName$ + " ระดับ 3 (ลด 10 Level)",L_Request3,"ใช้แบบ Cash",L_CashRequest;
		close;
	}
	else{
		L_Use:
		if(getd("$" + .variableName$ + "Buff") > 0){
			.@successChance = .lv1Chance;
			if(getd("$" + .variableName$ + "Buff") == 2)
			.@successChance = .lv2Chance;
			else if(getd("$" + .variableName$ + "Buff") == 3)
			.@successChance = .lv3Chance;
			else if(getd("$" + .variableName$ + "Buff") == 4)
			.@successChance = 100;
			callfunc("RandomOptionReflectDef",.@successChance,.npcName$);
			close;
		}
		else{
			mes .npcName$;
			mes "... ยังไม่มีใครมาเติม " + .resourceName$ + " ... \nลองเรียกเพื่อนที่เล่น " + .className$ + " มาสิ";
			close;
		}
	}
	
	L_AlreadyUsed:
	clear;
	message strcharinfo(0),"มีคนใช้ " + .systemName$ + " ในระดับที่สูงกว่าอยู่แล้ว จะไม่มีการเติม " + .resourceName$ + " แต่ Level ยังคงลดได้อยู่";
	return;
	
	L_Request1:
	if(getd("$" + .variableName$ + "Buff") > 0)
	callsub L_AlreadyUsed;
	else{
		set getd("$" + .variableName$ + "Buff"),1;
		set getd("$" + .variableName$ + "BuffTimer"),.normalTimer;
	}
	goto L_RequestTrade;
	
	L_Request2:
	if(getd("$" + .variableName$ + "Buff") > 1)
	callsub L_AlreadyUsed;
	else{
		set getd("$" + .variableName$ + "Buff"),2;
		set getd("$" + .variableName$ + "BuffTimer"),.normalTimer;
	}
	goto L_RequestTrade;
	
	L_Request3:
	if(getd("$" + .variableName$ + "Buff") > 2)
	callsub L_AlreadyUsed;
	else{
		set getd("$" + .variableName$ + "Buff"),3;
		set getd("$" + .variableName$ + "BuffTimer"),.normalTimer;
	}
	goto L_RequestTrade;
	
	L_RequestCheck:
	if(getd("$" + .variableName$ + "Buff") > 3){
		mes .npcName$;
		mes "ยังไม่สามารถเติม " + .resourceName$ + "(Cash) ได้เนื่องจากเวลาเก่ายังไม่หมด";
		close;
	}
	return;
	
	L_CashRequest:
	clear;
	callsub L_RequestCheck;
	mes .npcName$;
	mes "ว่าไง";
	next;
	menu "เปิดใช้ " + .systemName$ + " (" + .cashPrice + " Cash)",-;
	callsub L_RequestCheck;
	mes .npcName$;
	mes "โอ้ ได้สิ ถ้าเจ้ามี Cash เพียงพอ!";
	next;
	callsub L_RequestCheck;
	if(#CASHPOINTS >= .cashPrice){
		#CASHPOINTS -= .cashPrice;
		set getd("$" + .variableName$ + "Buff"),4;
		set getd("$" + .variableName$ + "BuffTimer"),.cashTimer;
		mes .npcName$;
		mes "เยี่ยม! \nมีเวลาใช้งาน " + .systemName$ + "(Cash) \nเป็นเวลา " + .cashTimer + " นาที";
		announce strcharinfo(0) + " เปิดใช้ " + .systemName$ + "(Cash) โดยมีเวลาใช้งาน " + .cashTimer + " นาที",bc_all,0xF5FF28;
		close;
	}
	else{
		mes .npcName$;
		mes "Cash ของเจ้าไม่เพียงพอ";
		close;
	}
	
	L_RequestTrade:
	if(BaseLevel > 10)
	BaseLevel -= 10;
	else
	BaseLevel = 1;
	mes .npcName$;
	mes "คุณได้ลด 10 Level จากการใช้งาน " + .systemName$;
	close;
	
OnTimer60000:
	if(getd("$" + .variableName$ + "BuffTimer") > 0){
		set getd("$" + .variableName$ + "BuffTimer"),getd("$" + .variableName$ + "BuffTimer") - 1;
		if(getd("$" + .variableName$ + "BuffTimer") <= 0)
		set getd("$" + .variableName$ + "Buff"),0;
	}
	else
	set getd("$" + .variableName$ + "Buff"),0;

	initnpctimer;
	end;
	
OnInit:
	.npcName$ = "^8328FF[นักสู้]^000000";
	
	.systemName$ = "ทำ Random Option กันสะท้อน";
	
	.resourceName$ = "น้ำยากันสะท้อน";
	
	.variableName$ = "randomOptionReflectDef";
	
	.className$ = "Class - Taekwon";
	
	.normalTimer = 5;	// Minutes
	.cashTimer = 50;	// Minutes
	
	.lv1LevelRequire = 99;
	.lv2LevelRequire = 190;
	
	.lv1Chance = 20;
	.lv2Chance = 40;
	.lv3Chance = 60;
	
	.cashPrice = 50;
	
	waitingroom .systemName$,0;
	initnpctimer;
	end; 
}

function	script	RandomOptionReflectDef	{
	.@chance = getarg(0,0);
	.@npcName$ = getarg(1,"^8328FF[ระบบ]^000000");
	.price = 100000;
	mes .@npcName$;
	mes "โอกาสสำเร็จ " + .@chance + "%";
	mes "*เมื่อ สำเร็จ Random Option ช่องที่เลือก จะกลายเป็น โดนสะท้อนเบาลง";
	mes "*เมื่อ ไม่สำเร็จ จะไม่มีผลเสียใด ๆ";
	mes "ราคาต่อรอบ " + callfunc("F_InsertComma",.price) + "z";
	next;
	
	setarray .@indices[1], EQI_HEAD_TOP, EQI_HEAD_MID, EQI_HEAD_LOW, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_COSTUME_HEAD_TOP, EQI_COSTUME_HEAD_MID, EQI_COSTUME_HEAD_LOW, EQI_SHADOW_ARMOR, EQI_SHADOW_WEAPON, EQI_SHADOW_SHIELD, EQI_COSTUME_GARMENT, EQI_SHADOW_SHOES, EQI_SHADOW_ACC_L, EQI_SHADOW_ACC_R;
	
	for(.@i = 1; .@i<getarraysize(.@indices); ++.@i) {
		if(getequipisequiped(.@indices[.@i])) {
			.@menu$ = .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			.@equipped = 1;
		}
		.@menu$ = .@menu$ + ":";
	}
	
	if (.@equipped == 0) {
		mes .@npcName$;
		mes "เจ้าไม่ได้สวมใส่ อุปกรณ์สวมใส่ใด ๆ เลย ข้าไม่สามารถทำอะไรได้";
		close;
	}
	
	.@part = .@indices[select(.@menu$)];

	if(!getequipisequiped(.@part)) {
		mes .@npcName$;
		mes "เจ้าเลือกช่องที่ ไม่ได้สวมใส่อะไรอยู่เลย";
		emotion ET_FRET;
		close;
	}
	
	.@selectedItemId = getequipid(.@part);

	mes .@npcName$;
	mes "เลือกช่อง Random Option 1-5";
	next;
	input .@index;
	.@index--;
	if(.@index <= 0)
	.@index = 0;
	else 	if(.@index >= 4)
	.@index = 4;

	menu "1 รอบ",L_Once,"กดอัตโนมัติจนกว่าจะติด",-;
	.@loop = 1;
	L_Once:
	
	freeloop(1);
	
	if(Zeny < .price) {
		mes .@npcName$;
		mes "เจ้ามี Zeny ไม่เพียงพอ";
		freeloop(0);
		close;
	}
	Zeny -= .price;

	if(rand(1,100) <= .@chance)
	.@success = 1;

	if(.@loop){
		.@sumZeny = Zeny;
		while(!.@success && .@sumZeny >= .price){
			.@sumZeny -= .price;
			if(rand(1,100) <= .@chance)
			.@success = 1;
		}
		Zeny = .@sumZeny;
	}

	freeloop(0);

	if (callfunc("F_IsEquipIDHack", .@part, .@selectedItemId)) {
		emotion ET_FRET;
		mes .@npcName$;
		mes "เหมือนว่าเจ้าจะสลับอุปกรณ์ ระหว่างข้าดำเนินการอยู่นะ";
		close;
	}

	if(.@success){
		mes .@npcName$;
		mes "เรียบร้อยละ ดูเอาเองเด้อ";
		
		.@index0 = getequiprandomoption(.@part,0,ROA_ID);
		.@index1 = getequiprandomoption(.@part,1,ROA_ID);
		.@index2 = getequiprandomoption(.@part,2,ROA_ID);
		.@index3 = getequiprandomoption(.@part,3,ROA_ID);
		.@index4 = getequiprandomoption(.@part,4,ROA_ID);
		
		.@valueIndex0 = getequiprandomoption(.@part,0,ROA_VALUE);
		.@valueIndex1 = getequiprandomoption(.@part,1,ROA_VALUE);
		.@valueIndex2 = getequiprandomoption(.@part,2,ROA_VALUE);
		.@valueIndex3 = getequiprandomoption(.@part,3,ROA_VALUE);
		.@valueIndex4 = getequiprandomoption(.@part,4,ROA_VALUE);

		.@randomOptionRand = 221;

		if(.@index0 <= 0)
		setrandomoption(.@part,0,1,1,0);
		if(.@index1 <= 0)
		setrandomoption(.@part,1,1,1,0);
		if(.@index2 <= 0)
		setrandomoption(.@part,2,1,1,0);
		if(.@index3 <= 0)
		setrandomoption(.@part,3,1,1,0);
		if(.@index4 <= 0)
		setrandomoption(.@part,4,1,1,0);

		.@valueOnSelectedIndex = getd(".@valueIndex" + .@index);
		if(.@valueOnSelectedIndex > 0)
		setrandomoption(.@part,.@index,.@randomOptionRand,.@valueOnSelectedIndex,0);
		else
		setrandomoption(.@part,.@index,.@randomOptionRand,1,0);
		
		emotion ET_SMILE;
		
		close;
	}
	else{
		mes .@npcName$;
		mes "ไม่ติด";
		close;
	}
	return;
}
