prontera,75,50,1	script	ห้องล่า Option Soul	604,{
	if (option_soul_hunt_cooldown > gettimetick(2)){
		mes "โปรดรอ " + (option_soul_hunt_cooldown - gettimetick(2)) + " วินาที";
		close;
	}
	mes "๐ คุณมี " + callfunc("GetGoodConditionColor") + callfunc("F_InsertComma",os) + " Option Soul^000000";
	mes "---";
	mes "๐ เป็น Map PK " + callfunc("GetBadConditionColor") + "(ฆ่ากันได้อิสระ คนฆ่าจะสุ่มได้รับ " + callfunc("GetKillPointsColor") + "Kill Points^000000" + callfunc("GetBadConditionColor") + " ของผู้ตายด้วย)^000000";
	mes "๐ เข้าได้ทุกคน " + callfunc("GetBadConditionColor") + "แต่ควรเข้าเมื่อคิดว่าพร้อมแล้วเท่านั้น^000000";
	mes "๐ Monster ในห้องมีทั้งหมด " + callfunc("F_InsertComma",.max_monster_amount) + " ตัว";
	mes "๐ Monster ในห้อง Level 160+";
	mes "๐ เมื่อกำจัด Monster จะ" + callfunc("GetGoodConditionColor") + "มีโอกาส 30% ที่จะได้^000000 " + callfunc("GetGoodConditionColor") + "Option Soul^000000";
	mes "๐ สามารถใช้ " + callfunc("GetGoodConditionColor") + callfunc("F_InsertComma",.trade_amount) + " Option Soul^000000 เพื่อแลก " + callfunc("GetGoodConditionColor") + "Option 1 แถว^000000";
	mes "๐ ทุก ๆ 1 นาที จะทำการ Respawn ให้ครบจำนวน";
	mes "๐ เมื่อเข้าแล้วต้องรอ " + .enter_cooldown + " วินาที จึงจะกดเข้าห้องนี้ได้อีกครั้ง";
	next;
	menu "เข้า",-,"แลก",L_Trade;
	announce strcharinfo(0) + " เข้าห้องล่า Option Soul!",bc_all,0xFFD500;
	option_soul_hunt_cooldown = gettimetick(2) + .enter_cooldown;
	warp .mapName$,0,0;
	close;
	
	L_Trade:
	if(!option_item_id)
	goto L_SetItemId;
	mes "คุณต้องการ Option ID: " + option_item_id;
	next;
	menu "ต่อไป",L_Start,"กำหนด Option",-;
	next;
	L_SetItemId:
	mes "โปรดระบุ Option ID ที่ต้องการ";
	mes "<URL>Option ID ทั้งหมด<INFO>https://raw.githubusercontent.com/kaninhot004/rathena/the-box-2/npc/the_box_2/Random%20Option%20Ids.txt</INFO></URL>";
	input .@i;
	.@is_had_opt_id = callfunc("IsHadRandomOptionId",.@i);

	if(.@is_had_opt_id)
	option_item_id = .@i;
	
	if(option_item_id <= 0)
	mes "Option ID ดังกล่าวไม่เปิดให้ใช้งาน โปรดใส่ Option ID อื่น";
	else
	mes "คุณต้องการ Option ID: " + option_item_id;
	close;
	
	L_Start:
	if(option_item_id <= 0){
		mes "คุณยังไม่ได้ระบุ Option ที่ต้องการ";
		close;
	}

	setarray .@indices[1], EQI_HEAD_TOP, EQI_HEAD_MID, EQI_HEAD_LOW, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_COSTUME_HEAD_TOP, EQI_COSTUME_HEAD_MID, EQI_COSTUME_HEAD_LOW, EQI_SHADOW_ARMOR, EQI_SHADOW_WEAPON, EQI_SHADOW_SHIELD, EQI_COSTUME_GARMENT, EQI_SHADOW_SHOES, EQI_SHADOW_ACC_L, EQI_SHADOW_ACC_R;
	
	for(.@i = 1; .@i<getarraysize(.@indices); ++.@i) {
		if(getequipisequiped(.@indices[.@i])) {
			.@menu$ = .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			.@equipped = 1;
		}
		.@menu$ = .@menu$ + ":";
	}
	
	if (.@equipped == 0) {
		mes "ไม่พบ อุปกรณ์สวมใส่ ที่กำลังสวมใส่อยู่";
		close;
	}
	
	.@part = .@indices[select(.@menu$)];

	if(!getequipisequiped(.@part)) {
		mes "คุณไม่ได้สวมใส่ อุปกรณ์สวมใส่ ณ บริเวณที่เลือก";
		close;
	}
	
	.@selectedItemId = getequipid(.@part);

	mes "โปรดระบุแถวที่ต้องการ";
	next;

	.@menu$ = "แถว 1:";
	
	for(.@i = 1; .@i < 5; ++.@i) {
		if(getequiprandomoption(.@part,.@i - 1,ROA_ID) > 0)
		.@menu$ += "แถว " + (.@i + 1);
		else
		break;
		.@menu$ = .@menu$ + ":";
	}
	
	.@slot = select(.@menu$);

	if(os < .trade_amount) {
		mes "ต้องการ " + callfunc("GetGoodConditionColor") + "Option Soul^000000 อีก " + callfunc("F_InsertComma",(.trade_amount - os));
		close;
	}
	os -= .trade_amount;

	if (callfunc("F_IsEquipIDHack", .@part, .@selectedItemId)) {
		mes "ตรวจพบการโกง";
		close;
	}
	
	// Temporary fix
	if(option_item_id == 203)
	option_item_id = 166;
	else if(option_item_id == 204)
	option_item_id = 167;
	
	setrandomoption(.@part,.@slot - 1,option_item_id,1,0);

	mes "สำเร็จ!";
	close;
	
	L_Spawn:
	.@isInit = getarg(0);
	if(.isInit)
	.@spawn_amount = .max_monster_amount;
	else{
		.@current_amount_left = getmapunits(BL_MOB,.mapName$);
		.@spawn_amount = .max_monster_amount - .@current_amount_left;
	}
	freeloop(1);
	for(.@i = 0; .@i < .@spawn_amount; .@i++){
		.@spawn_mob_id = $all_mob_160_plus_ids[rand(0,$all_mob_id_160_plus_size - 1)];
		while(!isspawn(.@spawn_mob_id))
		.@spawn_mob_id = $all_mob_160_plus_ids[rand(0,$all_mob_id_160_plus_size - 1)];
		monster .mapName$,0,0,"--ja--",.@spawn_mob_id,1,"ห้องล่า Option Soul::OnMonsterDead";
	}
	
	freeloop(0);
	return;
	
OnMonsterDead:
	if(!playerattached())
	end;

	if(rand(100) <= 29){
		os++;
		announce "คุณได้รับ 1 Option Soul | รวม " + callfunc("F_InsertComma",os) + " Option Soul!",bc_self,0xEBB531;
	}
	end;
	
OnTimer60000:
	callsub L_Spawn,0;
	initnpctimer;
	end;
	
OnInit:
	.enter_cooldown = 30;
	.trade_amount = 30;
	.max_monster_amount = 30;
	.mapName$ = "ba_lost";
	waitingroom "ห้องล่า Option Soul",0;
	callsub L_Spawn,1;
	initnpctimer;
	end;
}

ba_lost	mapflag	monster_noteleport
ba_lost	mapflag	noteleport
ba_lost	mapflag	nomemo
ba_lost	mapflag	nosave
ba_lost	mapflag	pvp
