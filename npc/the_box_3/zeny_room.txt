//tra_fild	mapflag	tb
//tra_fild	mapflag	tb2
//tra_fild	mapflag	battleground
//tra_fild	mapflag	gvg
tra_fild	mapflag	pvp
tra_fild	mapflag	novending
tra_fild	mapflag	nochat
tra_fild	mapflag	monster_noteleport
tra_fild	mapflag	noteleport
tra_fild	mapflag	nowarp
tra_fild	mapflag	nowarpto
tra_fild	mapflag	nosave
tra_fild	mapflag	nobranch
tra_fild	mapflag	notomb

prontera,163,208,1	script	Boss Zeny	10007,{
	mes "๐ ต้องการ ^FF2929" + F_InsertComma(.req_item_amount) + " " + getitemname(.req_item_id) + "^000000 ในการเข้าห้อง";
	mes "---";
	mes "โทษภายในห้อง";
	mes "๐ ^FF2929เป็นห้อง Battleground (PvP)^000000";
	mes "๐ ^FF2929ไม่สามารถกด Ctrl+โจมตี ได้ (ต้องคลิกเม้าส์ค้างไว้แทน)^000000";
	mes "๐ ^FF2929หากออกจากเกมจะกลับสู่เมืองหลัก^000000";
	mes "๐ ^FF2929Damage จะเหลือ 5%^000000";
	mes "๐ ^FF2929Flee จะเหลือ 1%^000000";
	mes "๐ ^FF2929Monster จะโจมตีแรงขึ้น 20 เท่า^000000";
	mes "---";
	mes "รางวัล";
	mes "๐ ^3629FF" + F_InsertComma(.reward_min_zeny) + "~" + F_InsertComma(.reward_max_zeny) + "z^000000";
	next;
	menu "เข้า",-;
	// Check Item
	if (countitem(.req_item_id) < .req_item_amount){
		mes getitemname(.req_item_id) + " ไม่เพียงพอ";
		close;
	}
	zeny_room_combo = 0;
	delitem .req_item_id,.req_item_amount;
	setpcblock PCBLOCK_ALL,true;
	setpcblock PCBLOCK_MOVE,false;
	addtimer 2000,strnpcinfo(3) + "::OnWarp";
	announce strcharinfo(0) + " เข้าห้อง Boss (Zeny)",bc_all;
	warp .map_name$,0,0;
	end;
	
OnWarp:
	setpcblock PCBLOCK_ALL,false;
	end;
	
OnCleanUp:
	killmonsterall .map_name$;
	$exp_room_killed = 0;
	callsub L_Spawn;
	end;
	
OnBossDead:
	$exp_room_killed--;

	// Unexpected error
	if(!playerattached())
	end;

	// Get Zeny
	if(zeny_room_combo){
		.@combo_min_zeny = .reward_min_zeny + (.reward_combo_zeny * zeny_room_combo);
		if(.@combo_min_zeny > .reward_max_zeny)
		.@reward_zeny = .reward_max_zeny;
		else
		.@reward_zeny = rand(.@combo_min_zeny,.reward_max_zeny);
	}
	else
	.@reward_zeny = rand(.reward_min_zeny,.reward_max_zeny);
	Zeny += .@reward_zeny;
	zeny_room_combo++;
	message strcharinfo(0),"ได้รับ " + F_InsertComma(.@reward_zeny) + "z (ฆ่าไปแล้ว " + F_InsertComma(zeny_room_combo) + " ตัว)";
	
	if($exp_room_killed <= 0){
		killmonsterall .map_name$;
		$exp_room_killed = 0;
		callsub L_Spawn;
	}
	end;
	
	L_Spawn:
	$exp_room_killed = .spawn_amount;
	for(.@i = 0; .@i < .spawn_amount; .@i++)
	monster .map_name$,0,0,"--ja--",$all_boss_ids[rand(0,($all_boss_id_size - 1))],1,strnpcinfo(0) + "::OnBossDead";
	return;
	
OnInit:
	.map_name$ = "tra_fild";
	
	.reward_combo_zeny = 10000;
	.reward_min_zeny = 100000;
	.reward_max_zeny = 1000000;
	
	.req_item_id = 40017;
	.req_item_amount = 99;
	
	.spawn_amount = 10;
	
	waitingroom "ห้อง Boss (Zeny)",0;
	
	callsub L_Spawn;
	
	end;
}
