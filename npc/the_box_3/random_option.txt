prontera,120,263,3	script	Random Option	10127,{
	mes "๐ ต้องการ ^FF2929" + F_InsertComma(.price) + "z^000000";
	mes "^3F28FF๐ รับ Random Option 5 แถว^000000";
	mes "^3F28FF๐ สุ่มค่าที่ได้รับ 1~5 ต่อแถว^000000";
	next;
	
	setarray .@indices[1], EQI_HEAD_TOP, EQI_HEAD_MID, EQI_HEAD_LOW, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_COSTUME_HEAD_TOP, EQI_COSTUME_HEAD_MID, EQI_COSTUME_HEAD_LOW, EQI_SHADOW_ARMOR, EQI_SHADOW_WEAPON, EQI_SHADOW_SHIELD, EQI_COSTUME_GARMENT, EQI_SHADOW_SHOES, EQI_SHADOW_ACC_L, EQI_SHADOW_ACC_R;
	
	for(.@i = 1; .@i<getarraysize(.@indices); ++.@i){
		if(getequipisequiped(.@indices[.@i])){
			.@menu$ = .@menu$ + F_getpositionname(.@indices[.@i]);
			.@equipped = 1;
		}
		.@menu$ = .@menu$ + ":";
	}
	
	if (.@equipped == 0){
		mes "เจ้าไม่ได้สวมใส่ อุปกรณ์สวมใส่ใด ๆ เลย";
		close;
	}
	
	.@part = .@indices[select(.@menu$)];

	if(!getequipisequiped(.@part)){
		mes "เจ้าเลือกช่องที่ ไม่ได้สวมใส่อะไรอยู่เลย";
		close;
	}
	
	.@selectedItemId = getequipid(.@part);
	
	if(Zeny < .price){
		mes "Zeny ไม่เพียงพอ";
		close;
	}
	
	if (callfunc("F_IsEquipIDHack",.@part,.@selectedItemId)){
		mes "เหมือนว่าเจ้า จะสลับอุปกรณ์ ระหว่างที่ข้า กำลังดำเนินการอยู่นะ!";
		close;
	}
	
	Zeny -= .price;
	
	setrandomoption(.@part,0,callfunc("GetRandomOptionId"),rand(1,5),0);
	setrandomoption(.@part,1,callfunc("GetRandomOptionId"),rand(1,5),0);
	setrandomoption(.@part,2,callfunc("GetRandomOptionId"),rand(1,5),0);
	setrandomoption(.@part,3,callfunc("GetRandomOptionId"),rand(1,5),0);
	setrandomoption(.@part,4,callfunc("GetRandomOptionId"),rand(1,5),0);

	mes "เรียบร้อยแล้ว!";
	close;

OnInit:
	.price = 3000000;
	waitingroom "เสริมแกร่งด้วย Random Option",0;
	end;
}

function	script	GetRandomOptionId	{
	.@optId = rand(1,254);
	
	freeloop(1);
	
	while((.@optId >= 76 && .@optId <= 85)
	|| (.@optId >= 175 && .@optId <= 184)
	|| .@optId == 203
	|| .@optId == 204)
	.@optId = rand(1,254);

	freeloop(0);
	
	return .@optId;
}
