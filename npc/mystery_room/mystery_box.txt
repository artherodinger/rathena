function	script	MysteryBox	{
	.@boxNumber = getarg(0);
	
	// Already opened
	if((isOpenMysteryBox - 1) >= mysteryBoxCash){
		message strcharinfo(0),"คุณเปิดกล่องสำหรับรอบนี้ไปแล้ว..";
		// Buy more?
		if(mysteryBoxCash < 3){
			.@sumCashPrice = 90 * (mysteryBoxCash + 1);
			if(.@sumCashPrice <= 90)
			.@sumCashPrice = 90;
			menu "ใช้ " + .@sumCashPrice + " Cash เพื่อเปิดกล่องได้มากขึ้น",-;
			if(#CASHPOINTS >= .@sumCashPrice){
				#CASHPOINTS -= .@sumCashPrice;
				mysteryBoxCash++;
				message strcharinfo(0),"ยินดีด้วย! คุณสามารถเปิดกล่องได้ " + (mysteryBoxCash + 1) + " กล่องต่อรอบ!!";
			}
			else
			message strcharinfo(0),"ต้องการ " + .@sumCashPrice + " Cash คุณมี " + callfunc("F_InsertComma",#CASHPOINTS) + " Cash";
		}
		end;
	}
	
	// Set open mystery box state
	isOpenMysteryBox++;
	
	// Set item id
	.@itemId = getd("$mysteryBox" + .@boxNumber + "ItemId");

	.@level = $mysteryLevel;
	if($mysteryMode == 1 || $mysteryMode == 3)
	.@level = $mysteryTapUserLv;

	// Dynamic value = Level + (0 ~ (Level / 2))
	.@dynamicValue = .@level + (rand(0,(.@level / 2)));
	.@randomizeValueIndex0 = rand(-.@dynamicValue,.@dynamicValue);
	.@dynamicValue = .@level + (rand(0,(.@level / 2)));
	.@randomizeValueIndex1 = rand(-.@dynamicValue,.@dynamicValue);
	.@dynamicValue = .@level + (rand(0,(.@level / 2)));
	.@randomizeValueIndex2 = rand(-.@dynamicValue,.@dynamicValue);
	.@dynamicValue = .@level + (rand(0,(.@level / 2)));
	.@randomizeValueIndex3 = rand(-.@dynamicValue,.@dynamicValue);
	.@dynamicValue = .@level + (rand(0,(.@level / 2)));
	.@randomizeValueIndex4 = rand(-.@dynamicValue,.@dynamicValue);
	
	// Boss reward
	if($mysteryBoss){
		.@randomizeValueIndex0 *= 2;
		.@randomizeValueIndex1 *= 2;
		.@randomizeValueIndex2 *= 2;
		.@randomizeValueIndex3 *= 2;
		.@randomizeValueIndex4 *= 2;
	}
	
	setarray .@OptID[0],callfunc("GetMysteryRandomOptionId"),callfunc("GetMysteryRandomOptionId"),callfunc("GetMysteryRandomOptionId"),callfunc("GetMysteryRandomOptionId"),callfunc("GetMysteryRandomOptionId");
	setarray .@OptVal[0],.@randomizeValueIndex0,.@randomizeValueIndex1,.@randomizeValueIndex2,.@randomizeValueIndex3,.@randomizeValueIndex4;
	setarray .@OptParam[0],0,0,0,0,0;
	
	getitem3 .@itemId,1,1,rand(0,20),0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;

	return;
}

function	script	GetMysteryRandomOptionId	{
	setarray .@allOptId[0],1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,147,148,149,150,151,152,153,154,155,156,157,158,159,160,161,162,163,164,165,166,167,168,169,170,171,172,187,188,189,190,191,192,193,219,220,221,247,251;
	.@sumOptId = .@allOptId[rand(0,getarraysize(.@allOptId) - 1)];
	return .@sumOptId;
}

bat_c01,94,96,5	script	กล่องปริศนา#1	10249,{
	.@boxNumber = atoi(strnpcinfo(2));
	callfunc("MysteryBox",.@boxNumber);
	end;
	
OnInit:
	hideonnpc strnpcinfo(0);
	end;
}

bat_c01,97,96,5	duplicate(กล่องปริศนา#1)	กล่องปริศนา#2	10249
bat_c01,100,96,5	duplicate(กล่องปริศนา#1)	กล่องปริศนา#3	10249
bat_c01,103,96,5	duplicate(กล่องปริศนา#1)	กล่องปริศนา#4	10249
