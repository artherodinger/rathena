bat_c01,97,86,5	script	เริ่มต่อสู้	464,{
	if($mysterySpawner == 2){
		message strcharinfo(0),"Monster ตัวเก่ายังอยู่ โปรดฆ่ามันเสียก่อน";
		end;
	}
	else if($mysterySpawner == 1){
		callsub L_AlreadyTapCheck;
		end;
	}
	else{
		message strcharinfo(0),"ขณะนี้อยู่ที่ระดับ: " + callfunc("F_InsertComma",$mysteryLevel);
		menu "เริ่มต่อสู้",-;
		// Unexpected error
		if($mysterySpawner != 0){
			callsub L_AlreadyTapCheck;
			end;
		}
		else{
			$mysterySpawner = 1;
			// Reset countdown to spawn
			$mysterySpawnerCountdown = 10;
			// Reset dead count
			$mysteryDeadCount = 0;
			// Reset tap users
			deletearray $mysteryTap[0],getarraysize($mysteryTap);
			// Set tap user level
			$mysteryTapUserLv = BaseLevel;
			// +1 Current Level
			$mysteryLevel++;
			// Every 10 level had 50% chance to spawn MvP (Double reward at the end)
			if($mysteryLevel % 10 == 0 && rand (0,100) <= 50)
			$mysteryBoss = 1;
			// Not MvP
			else
			$mysteryBoss = 0;
			// Set monster tier
			$monsterTier = $mysteryLevel / 30;
			// Minimum tier 1
			if($monsterTier <= 1)
			$monsterTier = 1;
			// Maximum tier 19
			if($monsterTier >= 19)
			$monsterTier = 19;
			// Game mode
			$mysteryMode = rand(0,100);
			// Announce
			if($mysteryMode <= 19) // 20%
			{
				mapannounce "bat_c01","Mode: ปกติ",0,0x93FF28;
				$mysteryMode = 0;
			}
			else if($mysteryMode <= 69) // 50%
			{
				mapannounce "bat_c01","Mode: ตามเลเวลผู้กด",0,0x93FF28;
				$mysteryMode = 1;
			}
			else if($mysteryMode <= 84) // 15%
			{
				mapannounce "bat_c01","Mode: ปกติมหาศาล",0,0x93FF28;
				$mysteryMode = 2;
			}
			else if($mysteryMode <= 100) // 15%
			{
				mapannounce "bat_c01","Mode: ตามเลเวลผู้กดมหาศาล",0,0x93FF28;
				$mysteryMode = 3;
			}
			// Start countdown timer
			initnpctimer;
		}
		end;
	}
	
	// Countdown to spawn monster
OnTimer1000:
	$mysterySpawnerCountdown--;
	// Timeout
	if($mysterySpawnerCountdown <= 0){
		// Hide item reward
		hideonnpc "กล่องปริศนา#1";
		hideonnpc "กล่องปริศนา#2";
		hideonnpc "กล่องปริศนา#3";
		hideonnpc "กล่องปริศนา#4";
		// Hide npc
		hideonnpc "เริ่มต่อสู้";
		// Set monster alive
		$mysterySpawner = 2;
		// Set countdown to 0
		$mysterySpawnerCountdown = 0;
		// Set mystery box
		if(rand(0,1) == 0)
		$mysteryBox1ItemId = $allWeaponId[rand(0,$allWeaponIdArraySize - 1)];
		else
		$mysteryBox1ItemId = $allArmorId[rand(0,$allArmorIdArraySize - 1)];
		if(rand(0,1) == 0)
		$mysteryBox2ItemId = $allWeaponId[rand(0,$allWeaponIdArraySize - 1)];
		else
		$mysteryBox2ItemId = $allArmorId[rand(0,$allArmorIdArraySize - 1)];
		if(rand(0,1) == 0)
		$mysteryBox3ItemId = $allWeaponId[rand(0,$allWeaponIdArraySize - 1)];
		else
		$mysteryBox3ItemId = $allArmorId[rand(0,$allArmorIdArraySize - 1)];
		if(rand(0,1) == 0)
		$mysteryBox4ItemId = $allWeaponId[rand(0,$allWeaponIdArraySize - 1)];
		else
		$mysteryBox4ItemId = $allArmorId[rand(0,$allArmorIdArraySize - 1)];
		// Monster set
		if($mysteryMode == 0 || $mysteryMode == 2)
		.@sumMonsterSet = $monsterTier;
		else if($mysteryMode == 1 || $mysteryMode == 3)
		{
			.@sumMonsterSet = $mysteryTapUserLv / 30;
			// Minimum tier 1
			if(.@sumMonsterSet <= 1)
			.@sumMonsterSet = 1;
		}
		// Set monster id (Change monster set every 30 level)
		$mysteryMonsterId = getd("$mr" + .@sumMonsterSet + "id[" + rand(0,getarraysize(getd("$mr" + .@sumMonsterSet + "id")) - 1) + "]");
		// Force to spawn boss
		if($mysteryBoss)
		$mysteryMonsterId = $allMvp[rand(0,getarraysize($allMvp)-1)];
		// Monster count
		.@sumSpawnCount = 1;
		.@sumLevel = $mysteryLevel;
		if($mysteryMode == 1 || $mysteryMode == 3)
		.@sumLevel = $mysteryTapUserLv;
		while(.@sumLevel > 100)
		.@sumLevel -= 100;
		.@sumSpawnCount = .@sumLevel / 10;
		if(.@sumSpawnCount <= 1)
		.@sumSpawnCount = 1;
		if(rand(0,1) == 0)
		.@sumSpawnCount *= 2;
		// Crazy spawn mode
		if($mysteryMode == 1 || $mysteryMode == 3)
		.@sumSpawnCount = 300;
		$mysteryRoomSpawnCount = .@sumSpawnCount;
		// Spawn now
		callsub L_SpawnMonster;
		// Announce
		mapannounce "bat_c01",getmonsterinfo($mysteryMonsterId,MOB_NAME) + " ได้ปรากฎขึ้นมาแล้ว!!",0,0xFF5733;
		// Stop countdown
		stopnpctimer;
	}
	// Information for players
	else{
		// Announce
		mapannounce "bat_c01","เตรียมตัว.. Monster จะมาภายใน " + $mysterySpawnerCountdown + " วินาที!!",0,0xFF5733;
		// Continue countdown
		initnpctimer;
	}
	end;
	
	// Player tap to reduce spawn countdown
	L_AlreadyTapCheck:
	// Already tap
	for(.@i = 0; .@i < getarraysize($mysteryTap); .@i++){
		if(getcharid(3) == $mysteryTap[.@i]){
			message strcharinfo(0),"คุณกดเร่งเวลาไปแล้ว";
			end;
		}
	}
	
	// Check still had timer to reduce
	if($mysterySpawnerCountdown > 0){
		// Reduce countdown to spawn
		$mysterySpawnerCountdown -= 5;
		// Set tap users
		setarray $mysteryTap[getarraysize($mysteryTap)],getcharid(3);
	}
	return;
	
	L_SpawnMonster:
	$mysteryRoomKilled = 0;
	for(.@i = 0; .@i < $mysteryRoomSpawnCount; .@i++){
		monster "bat_c01",rand(96,110),rand(96,110),"--ja--",$mysteryMonsterId,1,strnpcinfo(0) + "::OnMonsterDead";
		
		getunitdata $@mobid[0],.@tempData;
		
		/*npctalk "HP: " + .@tempData[2];
	npctalk "Max HP: " + .@tempData[3];
	npctalk "Speed: " + .@tempData[8];
	npctalk "Str: " + .@tempData[24];
	npctalk "Agi: " + .@tempData[25];
	npctalk "Vit: " + .@tempData[26];
	npctalk "Int: " + .@tempData[27];
	npctalk "Dex: " + .@tempData[28];
	npctalk "Luk: " + .@tempData[29];
	npctalk "Atk Range: " + .@tempData[32];
	npctalk "Atk Min: " + .@tempData[33];
	npctalk "Atk Max: " + .@tempData[34];
	npctalk "MAtk Min: " + .@tempData[35];
	npctalk "MAtk Max: " + .@tempData[36];
	npctalk "Def: " + .@tempData[37];
	npctalk "MDef: " + .@tempData[38];
	npctalk "Hit: " + .@tempData[39];
	npctalk "Flee: " + .@tempData[40];
	npctalk "P.Dodge: " + .@tempData[41];
	npctalk "Cri: " + .@tempData[42];
	
	npctalk "---";*/
		
		.@tempHp = .@tempData[2];
		.@tempMaxHP = .@tempData[3];
		.@tempSpeed = .@tempData[8];
		.@tempStr = .@tempData[24];
		.@tempAgi = .@tempData[25];
		.@tempVit = .@tempData[26];
		.@tempInt = .@tempData[27];
		.@tempDex = .@tempData[28];
		.@tempLuk = .@tempData[29];
		.@tempAtkRange = .@tempData[32];
		.@tempAtkMin = .@tempData[33];
		.@tempAtkMax = .@tempData[34];
		.@tempMAtkMin = .@tempData[35];
		.@tempMAtkMax = .@tempData[36];
		.@tempDef = .@tempData[37];
		.@tempMDef = .@tempData[38];
		.@tempHit = .@tempData[39];
		.@tempFlee = .@tempData[40];
		.@tempPDodge = .@tempData[41];
		.@tempCri = .@tempData[42];
		
		.@multiplier = 1;
		
		.@sumLevel = $mysteryLevel;
		while(.@sumLevel > 30)
		.@sumLevel -= 30;
		
		.@multiplier = .@sumLevel / 10;

		if(.@multiplier <= 1)
		.@multiplier = 1;
		
		.@multiplier2 = .@multiplier / 2;
		if(.@multiplier2 <= 1)
		.@multiplier2 = 1;

		setunitdata $@mobid[0],UMOB_HP,.@tempHp * .@multiplier;
		setunitdata $@mobid[0],UMOB_MAXHP,.@tempMaxHP * .@multiplier;
		setunitdata $@mobid[0],UMOB_SPEED,.@tempSpeed * .@multiplier;
		setunitdata $@mobid[0],UMOB_MODE,(0x0000001 | 0x0000004 | 0x0000080);
		setunitdata $@mobid[0],UMOB_STR,.@tempStr * .@multiplier;
		setunitdata $@mobid[0],UMOB_AGI,.@tempAgi * .@multiplier;
		setunitdata $@mobid[0],UMOB_VIT,.@tempVit * .@multiplier;
		setunitdata $@mobid[0],UMOB_INT,.@tempInt * .@multiplier;
		setunitdata $@mobid[0],UMOB_DEX,.@tempDex * .@multiplier;
		setunitdata $@mobid[0],UMOB_LUK,.@tempLuk * .@multiplier;
		setunitdata $@mobid[0],UMOB_ATKRANGE,.@tempAtkRange * .@multiplier;
		setunitdata $@mobid[0],UMOB_ATKMIN,.@tempAtkMin * .@multiplier;
		setunitdata $@mobid[0],UMOB_ATKMAX,.@tempAtkMax * .@multiplier;
		setunitdata $@mobid[0],UMOB_MATKMIN,.@tempMAtkMin * .@multiplier;
		setunitdata $@mobid[0],UMOB_MATKMAX,.@tempMAtkMax * .@multiplier;
		setunitdata $@mobid[0],UMOB_DEF,.@tempDef * .@multiplier;
		setunitdata $@mobid[0],UMOB_MDEF,.@tempMDef * .@multiplier;
		setunitdata $@mobid[0],UMOB_HIT,.@tempHit * .@multiplier;
		setunitdata $@mobid[0],UMOB_FLEE,.@tempFlee * .@multiplier2;
		setunitdata $@mobid[0],UMOB_PDODGE,.@tempPDodge * .@multiplier;
		setunitdata $@mobid[0],UMOB_CRIT,.@tempCri * .@multiplier;
		
		/*getunitdata $@mobid[0],.@tempData;
	
	npctalk "HP: " + .@tempData[2];
	npctalk "Max HP: " + .@tempData[3];
	npctalk "Speed: " + .@tempData[8];
	npctalk "Str: " + .@tempData[24];
	npctalk "Agi: " + .@tempData[25];
	npctalk "Vit: " + .@tempData[26];
	npctalk "Int: " + .@tempData[27];
	npctalk "Dex: " + .@tempData[28];
	npctalk "Luk: " + .@tempData[29];
	npctalk "Atk Range: " + .@tempData[32];
	npctalk "Atk Min: " + .@tempData[33];
	npctalk "Atk Max: " + .@tempData[34];
	npctalk "MAtk Min: " + .@tempData[35];
	npctalk "MAtk Max: " + .@tempData[36];
	npctalk "Def: " + .@tempData[37];
	npctalk "MDef: " + .@tempData[38];
	npctalk "Hit: " + .@tempData[39];
	npctalk "Flee: " + .@tempData[40];
	npctalk "P.Dodge: " + .@tempData[41];
	npctalk "Cri: " + .@tempData[42];*/
	}
	return;
	
	// Any monster dead goes here
OnMonsterDead:
	$mysteryRoomKilled++;
	if($mysteryRoomKilled < $mysteryRoomSpawnCount)
	{
		// Announce
		mapannounce "bat_c01","ฆ่าแล้ว " + $mysteryRoomKilled + "/" + $mysteryRoomSpawnCount,0;
		end;
	}
	// Set monster dead
	$mysterySpawner = 0;
	// Show npc
	hideoffnpc "เริ่มต่อสู้";
	// Sum EXP = Base EXP * Level
	.sumEXP = .baseEXP * $mysteryLevel;
	// Dynamic EXP ( 0 ~ (Sum EXP / 2))
	.@dynamicEXP = rand(0,(.sumEXP / 2));
	// Unexpected error
	if(.@dynamicEXP <= 0)
	.@dynamicEXP = 0;
	// No death bonus
	if($mysteryDeadCount <= 0){
		.@isNoDeathBonus = 1;
		// Dynamic EXP ( 0 ~ (EXP / 5))
		.@noDeathEXP = rand(0,(.sumEXP / 5));
		// Unexpected error
		if(.@noDeathEXP <= 0)
		.@noDeathEXP = 0;
	}
	.sumEXP += .@dynamicEXP + .@noDeathEXP;
	// Boss bonus
	if($mysteryBoss)
	.sumEXP *= 2;
	// Show item reward
	hideoffnpc "กล่องปริศนา#1";
	hideoffnpc "กล่องปริศนา#2";
	hideoffnpc "กล่องปริศนา#3";
	hideoffnpc "กล่องปริศนา#4";
	// Announce
	mapannounce "bat_c01",getmonsterinfo($mysteryMonsterId,MOB_NAME) + " ถูกฆ่าหมดแล้ว!! ทุกคนภายใน Map ได้รับ " + callfunc("F_InsertComma",.sumEXP) + " EXP " + callfunc("F_InsertComma",.sumEXP/2) + " Job EXP",0;
	// Give EXP to all player in map
	addrid(5,0,"bat_c01");
	getexp .sumEXP,.sumEXP/2;
	Zeny += $mysteryLevel * 40;
	specialeffect2 399;
	isOpenMysteryBox = 0;
	end;
	
	// Initialize
OnInit:
	.baseEXP = 100;
	// Respawn monster here
	if($mysterySpawner == 2)
	callsub L_SpawnMonster;
	// Reset count timer here
	else if($mysterySpawner == 1)
	$mysterySpawner = 0;
	// Hide / Show here
	if($mysterySpawner <= 0)
	hideoffnpc "เริ่มต่อสู้";
	else
	hideonnpc "เริ่มต่อสู้";
	end;
}
