-	script	MysteryBoss	-1,{
	end;
	
OnPCDieEvent:
	if(strcharinfo(3) == .map_name$){
		$mystery_boss_pdc++;
		callsub L_RefreshVariable;
		callsub L_Spawn;
	}
	end;
	
OnBossDead:
	.@chance = rand(0,$mystery_boss_amount);
	if(.@chance > 100)
	.@chance = 100;

	if(rand(0,100) <= .@chance){
		getitem .reward_id,1;
		announce "ยินดีด้วย! คุณได้รับ " + getitemname(.reward_id) + "!",0xEBB531|bc_self;
	}
	else
	announce "เสียใจด้วย! คุณไม่ได้รับ " + getitemname(.reward_id),0xEBB531|bc_self;

	$mystery_boss_killed++;
	.@spawnCount = $mystery_boss_amount;
	if(.@spawnCount > 10)
	.@spawnCount = 10;
	if($mystery_boss_killed >= .@spawnCount){
		killmonsterall .map_name$;
		$mystery_boss_amount++;
		$mystery_boss_killed = 0;
		callsub L_Spawn;
		mapannounce .map_name$,"Boss ตายหมดแล้ว ยกระดับเกมทันที!! โอกาสได้รับ " + getitemname(.reward_id) + " มากสุด " + $mystery_boss_amount + "%",bc_map;
	}
	end;
	
	L_RefreshVariable:
	if(!$mystery_boss_amount)
	$mystery_boss_amount = 1;
	
	// Check player dead count
	if($mystery_boss_pdc >= .pdc){
		mapannounce .map_name$,"ผู้เล่นตายเกินจำนวนที่กำหนด เริ่มเกมใหม่!",bc_map;

		// Set boss amount to 1
		$mystery_boss_amount = 1;
		// Set boss dead count to 0
		$mystery_boss_killed = 0;
		// Set player dead count to 0
		$mystery_boss_pdc = 0;
		
		killmonsterall .map_name$;
	}
	return;
	
	L_Spawn:
	.@spawnCount = $mystery_boss_amount;
	if(.@spawnCount > 10)
	.@spawnCount = 10;
	for(.@i = 0; .@i < .@spawnCount; .@i++){
		monster .map_name$,0,0,"--ja--",.boss_id[rand(0,(.boss_id_size - 1))],1,strnpcinfo(0) + "::OnBossDead";
		
		// Random Ignore Mode (Ignore Magic, Ignore Melee, Ignore Misc, Ignore Ranged)
		.@rand = rand(0,3);
		if(.@rand == 0)
		setunitdata $@mobid[0],UMOB_MODE,(0x0000001 | 0x0000004 | 0x0000080 | 0x0010000);
		else if(.@rand == 1)
		setunitdata $@mobid[0],UMOB_MODE,(0x0000001 | 0x0000004 | 0x0000080 | 0x0020000);
		else if(.@rand == 2)
		setunitdata $@mobid[0],UMOB_MODE,(0x0000001 | 0x0000004 | 0x0000080 | 0x0040000);
		else if(.@rand == 3)
		setunitdata $@mobid[0],UMOB_MODE,(0x0000001 | 0x0000004 | 0x0000080 | 0x0100000);
	}
	return;
	
OnInit:
	setarray .boss_id[0],2564,1090,1092,1088,1086,1059,1147,1159,1112,1630,1296,1312,1492,1038,1157,1511,1418,1388,1582,1096,1120,1090,1658,1046,1785,1087,1190,1039,1272,1871,1832,1751,1873,1956,2022,1087,1929,2564,3180,3181,2320,2321,2322,2317,2319,1087,1147,1190,1115,1086,1038,1159,1389,1046,1059,1150,1688,1039,1089,1092,1088,1096,1093,1120,1090,1980,1157,1112,1251,2068,1373,2156,1272,1630,1252,1779,1708,1623,1418,1312,1785,1734,1719,1768,2087,1751,2253,2255,1832,1874,3124,2942,2532,2529,2535,2533,2534,2959,2961,2131,3628,2996,2997,3029,3254,2187,2188,2189,2190,2998,2999,3000,3073,2194,2475,2476,3450,3190,2542,3484,3474,3475,3473,3621,1646,1650,2239,1647,2241,2238,2240,2236,2235,2237,1651,1649,1648,1871,1873,1719,1492,1147,1688,1583,2068,3074,2156,2087,2165,1623,1389,1046,1272,1272,1150,1115,2253,1059,1147,2251,1039,1785,2249,1046,1272,2255,1418,1157,2202,1685,1734,1658,1630,1038,1511,2362,1885,1751,1150,1039,1086,1768,3741,2441,2442,1832,1112,1312,1251,1087,1190,1252,1059,1159,1917,1115,1785,1785,1785,1785;
	
	.boss_id_size = getarraysize(.boss_id);
	
	.pdc = 5;
	
	.reward_id = 40016;
	
	.map_name$ = "x_prt";
	
	callsub L_RefreshVariable;
	
	callsub L_Spawn;
	
	end;
}

x_prt	mapflag	noteleport
x_prt	mapflag	nocommand	99
x_prt	mapflag	nomemo
x_prt	mapflag	nosave

prontera,163,203,5	script	Mystery Boss	459,{
	mes "วิธีเล่น";
	mes "๐ เข้าไปสู้ Boss";
	mes "๐ ฆ่าได้ ^7A31EB" + getitemname(.reward_id) + "^000000 (1~^D10687"+ $mystery_boss_amount +"^000000)%";
	mes "๐ มันจะสุ่ม^A15008การป้องกัน^000000 {^EC1500ตีใกล้, ตีไกล, ตีเวทย์, อื่น ๆ^000000}";
	next;
	menu "เข้า",-;
	warp "x_prt",0,0;
	close;
	
OnInit:
	.reward_id = 40016;
	waitingroom "Mystery Boss",0;
	end;
}
