prontera,159,214,8	script	Boss Equipment	10007,{
	mes "๐ ต้องการ ^FF2929" + F_InsertComma(.req_zeny) + "z^000000 \nและ ^FF2929" + F_InsertComma(.req_item_amount) + " " + getitemname(.req_item_id) + "^000000 ในการเข้าห้อง";
	mes "---";
	mes "โทษภายในห้อง";
	mes "๐ ^FF2929ไม่สามารถกด Ctrl+โจมตี ได้ (ต้องคลิกเม้าส์ค้างไว้แทน)";
	mes "๐ ^FF2929ไม่สามารถ Teleport ได้";
	mes "๐ ^FF2929ไม่สามารถ Warp ได้";
	mes "๐ ^FF2929ไม่สามารถ Memo ได้";
	mes "๐ ^FF2929ไม่สามารถใช้ ไม้ผี ได้";
	mes "๐ ^FF2929หากออกจากเกมจะกลับสู่เมืองหลัก";
	mes "๐ ^FF2929Damage จะเหลือ 20%";
	mes "๐ ^FF2929Flee จะเหลือ 10%";
	mes "๐ ^FF2929Monster จะโจมตีแรงขึ้น 10 เท่า";
	mes "---";
	mes "รางวัล";
	mes "๐ ^3629FFเมื่อกำจัด Monster จะได้รับ Equipment ที่ต้องใส่ตอน Level 99 ขึ้นไป";
	next;
	menu "เข้า",-;
	// Check Zeny
	if (Zeny < .req_zeny){
		mes "Zeny ไม่เพียงพอ";
		close;
	}
	// Check Item
	if (countitem(.req_item_id) < .req_item_amount){
		mes getitemname(.req_item_id) + " ไม่เพียงพอ";
		close;
	}
	Zeny -= .req_zeny;
	delitem .req_item_id,.req_item_amount;
	specialeffect2 EF_MAXPOWER;
	warp .map_name$,0,0;
	end;
	
OnCleanUp:
	killmonsterall .map_name$;
	if(getmapunits(BL_MOB,.map_name$) <= 0){
		$high_lv_equip_room_killed = 0;
		callsub L_Spawn;
	}
	end;
	
OnBossDead:
	$high_lv_equip_room_killed--;

	// Unexpected error
	if(!playerattached())
	end;

	// Get equipment
	freeloop(1);
	
	.@rand = rand(1,3);
	if(.@rand == 1){
		.@item_id = $weaponIds[rand(0,($weaponIdArraySize - 1))];
		while(getiteminfo(.@item_id,ITEMINFO_EQUIPLEVELMIN) < 99)
		.@item_id = $weaponIds[rand(0,($weaponIdArraySize - 1))];
	}
	else if(.@rand == 2){
		.@item_id = $equipmentIds[rand(0,($equipmentIdArraySize - 1))];
		while(getiteminfo(.@item_id,ITEMINFO_EQUIPLEVELMIN) < 99)
		.@item_id = $equipmentIds[rand(0,($equipmentIdArraySize - 1))];
	}
	else if(.@rand == 3){
		.@item_id = $costumeIds[rand(0,($costumeIdArraySize - 1))];
		while(getiteminfo(.@item_id,ITEMINFO_EQUIPLEVELMIN) < 99)
		.@item_id = $costumeIds[rand(0,($costumeIdArraySize - 1))];
	}
	
	freeloop(0);
	
	announce strcharinfo(0) + " ได้รับ " + getitemname(.@item_id),bc_map;
	
	getitem .@item_id,1;
	
	if($high_lv_equip_room_killed <= 0){
		killmonsterall .map_name$;
		$high_lv_equip_room_killed = 0;
		callsub L_Spawn;
	}
	end;
	
	L_Spawn:
	$high_lv_equip_room_killed = .spawn_amount;
	for(.@i = 0; .@i < .spawn_amount; .@i++)
	monster .map_name$,0,0,"--ja--",$all_boss_ids[rand(0,($all_boss_id_size - 1))],1,strnpcinfo(0) + "::OnBossDead";
	return;
	
OnInit:
	.map_name$ = "lasa_sea";
	
	.req_zeny = 900000;
	.req_item_id = 40017;
	.req_item_amount = 9;
	
	.spawn_amount = 20;
	
	waitingroom "ห้องล่า Boss (Equipment Lv.99+)",0;
	
	callsub L_Spawn;
	
	end;
}

lasa_sea	mapflag	noteleport
lasa_sea	mapflag	nowarp
lasa_sea	mapflag	nowarpto
lasa_sea	mapflag	nosave
lasa_sea	mapflag	nobranch
//lasa_sea	mapflag	battleground
lasa_sea	mapflag	gvg
lasa_sea	mapflag	notomb
