prontera,148,207,5	script	Boss EXP	10007,{
	mes "๐ ต้องการ ^FF2929" + F_InsertComma(.req_zeny) + "z^000000 \nและ ^FF2929" + F_InsertComma(.req_item_amount) + " " + getitemname(.req_item_id) + "^000000 ในการเข้าห้อง";
	mes "---";
	mes "โทษภายในห้อง";
	mes "๐ ^FF2929ไม่สามารถกด Ctrl+โจมตี ได้ (ต้องคลิกเม้าส์ค้างไว้แทน)";
	mes "๐ ^FF2929ไม่สามารถ Teleport ได้";
	mes "๐ ^FF2929ไม่สามารถ Warp ได้";
	mes "๐ ^FF2929ไม่สามารถ Memo ได้";
	mes "๐ ^FF2929ไม่สามารถใช้ ไม้ผี ได้";
	mes "๐ ^FF2929หากออกจากเกมจะกลับสู่เมืองหลัก";
	mes "๐ ^FF2929Damage จะเหลือ 5%";
	mes "๐ ^FF2929Flee จะเหลือ 1%";
	mes "๐ ^FF2929Monster จะโจมตีแรงขึ้น 20 เท่า";
	mes "---";
	mes "รางวัล";
	mes "๐ ^3629FFEXP พิเศษ " + F_InsertComma(.exp_bonus) + " เมื่อกำจัด Monster (เพิ่ม EXP ได้ปกติ)";
	next;
	menu "เข้า",-;
	// Check Zeny
	if (Zeny < .req_zeny){
		mes "Zeny ไม่เพียงพอ";
		close;
	}
	// Check Item
	if (countitem(.req_item_id) < .req_item_amount){
		mes getitemname(.req_item_id) + " ไม่เพียงพอ";
		close;
	}
	Zeny -= .req_zeny;
	delitem .req_item_id,.req_item_amount;
	specialeffect2 EF_MAXPOWER;
	warp .map_name$,0,0;
	end;
	
OnCleanUp:
	killmonsterall .map_name$;
	if(getmapunits(BL_MOB,.map_name$) <= 0){
		$exp_room_killed = 0;
		callsub L_Spawn;
	}
	end;
	
OnBossDead:
	$exp_room_killed--;

	// Unexpected error
	if(!playerattached())
	end;

	getexp .exp_bonus,.exp_bonus;

	if($exp_room_killed <= 0){
		killmonsterall .map_name$;
		$exp_room_killed = 0;
		callsub L_Spawn;
	}
	end;
	
	L_Spawn:
	$exp_room_killed = .spawn_amount;
	for(.@i = 0; .@i < .spawn_amount; .@i++)
	monster .map_name$,0,0,"--ja--",$all_boss_ids[rand(0,($all_boss_id_size - 1))],1,strnpcinfo(0) + "::OnBossDead";
	return;
	
OnInit:
	.map_name$ = "tra_fild";
	
	.exp_bonus = 190000000;
	
	.req_zeny = 1900000;
	.req_item_id = 40017;
	.req_item_amount = 19;
	
	.spawn_amount = 10;
	
	waitingroom "ห้องล่า Boss (EXP)",0;
	
	callsub L_Spawn;
	
	end;
}

tra_fild	mapflag	noteleport
tra_fild	mapflag	nowarp
tra_fild	mapflag	nowarpto
tra_fild	mapflag	nosave
tra_fild	mapflag	nobranch
tra_fild	mapflag	battleground
//tra_fild	mapflag	gvg
tra_fild	mapflag	notomb
