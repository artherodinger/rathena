prontera,174,178,3	script	Upgrade สุ่ม Option	869,{
	if(#ro_max_value > .max_value){
		mes $hint_color$ + "คุณ Upgrade ถึงขั้นสูงสุดแล้ว (" + .max_value + ") (ใช้ร่วมกันทั้ง ID)" + $end_color$;
		close;
	}
	
	if(#ro_max_value > 399)
	.@price = .price_greater_399;
	else if(#ro_max_value < 10)
	.@price = .price_below_10;
	else if(#ro_max_value < 20)
	.@price = .price_below_20;
	else if(#ro_max_value < 30)
	.@price = .price_below_30;
	else if(#ro_max_value < 40)
	.@price = .price_below_40;
	else if(#ro_max_value < 50)
	.@price = .price_below_50;
	else if(#ro_max_value < 100)
	.@price = .price_below_100;
	else if(#ro_max_value < 150)
	.@price = .price_below_150;
	else if(#ro_max_value < 200)
	.@price = .price_below_200;
	else if(#ro_max_value < 300)
	.@price = .price_below_300;
	else if(#ro_max_value < 400)
	.@price = .price_below_400;
	else
	.@price = .price_max;
	
	callsub L_CurrentValue;
	next;
	menu "Upgrade (" + $currency_color$ + .@price + "z" + $end_color$ + ")",-,"Upgrade ทีเดียวเต็ม (" + $currency_color$ + .price_max + "z" + $end_color$ + ")",L_Max;
	if(Zeny < .@price){
		mes "คุณมี Zeny ไม่เพียงพอ";
		close;
	}
	Zeny -= .@price;
	#ro_max_value++;
	callsub L_CurrentValue;
	close;
	
	L_Max:
	if(Zeny < .price_max){
		mes "คุณมี Zeny ไม่เพียงพอ";
		close;
	}
	Zeny -= .price_max;
	#ro_max_value = .max_value;
	callsub L_CurrentValue;
	close;
	
	L_CurrentValue:
	mes "ค่ามากสุดที่จะสุ่มได้: " + $hint_color$ + #ro_max_value + $end_color$ + " (ใช้ร่วมกันทั้ง ID)";
	return;
	
OnInit:
	.price_below_10 = 3;
	.price_below_20 = 9;
	.price_below_30 = 30;
	.price_below_40 = 300;
	.price_below_50 = 1300;
	.price_below_100 = 1900;
	.price_below_150 = 3300;
	.price_below_200 = 9300;
	.price_below_300 = 13000;
	.price_below_400 = 19000;
	.price_greater_399 = 33000;
	.price_max = 30000000;
	.max_value = 500;
	waitingroom "Upgrade สุ่ม Option",0;
	end;
}

prontera,176,176,3	script	สุ่ม Option	10267,{
	// Is not had max value?
	if(!#ro_max_value){
		mes "โปรด Upgrade ค่าการสุ่มมากสุดเสียก่อน";
		close;
	}
	// Hint max value
	mes "ใช้ " + $currency_color$ + .price + "z" + $end_color$ + " ต่อการสุ่ม 1 ครั้ง";
	mes $hint_color$ + "๐ ค่าที่จะสุ่มได้: 0~" + $reward_color$ + #ro_max_value + $end_color$;
	mes $important_color$ + "๐ ไม่สามารถเลือกแถวได้" + $end_color$;
	next;
	// Setup
	setarray .@indices[1],EQI_HEAD_TOP,EQI_HEAD_MID,EQI_HEAD_LOW,EQI_ARMOR,EQI_HAND_L,EQI_HAND_R,EQI_GARMENT,EQI_SHOES,EQI_ACC_L,EQI_ACC_R,EQI_COSTUME_HEAD_TOP,EQI_COSTUME_HEAD_MID,EQI_COSTUME_HEAD_LOW,EQI_SHADOW_ARMOR,EQI_SHADOW_WEAPON,EQI_SHADOW_SHIELD,EQI_COSTUME_GARMENT,EQI_SHADOW_SHOES,EQI_SHADOW_ACC_L,EQI_SHADOW_ACC_R;
	for(.@i = 1; .@i < getarraysize(.@indices); ++.@i)
	{
		if(getequipisequiped(.@indices[.@i])){
			.@menu$ = .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			.@equipped = 1;
		}
		.@menu$ = .@menu$ + ":";
	}
	// Is had any equipments?
	if (!.@equipped){
		mes "คุณไม่ได้สวมใส่ อุปกรณ์สวมใส่ ใด ๆ เลย";
		close;
	}
	// Select
	.@part = .@indices[select(.@menu$)];
	// Unexpected error
	if(!getequipisequiped(.@part)) {
		mes "คุณเลือกช่องที่ ไม่มีอุปกรณ์สวมใส่ สวมใส่อยู่";
		close;
	}
	// Remember item id
	.@selectedItemId = getequipid(.@part);
	// Trade
	if(Zeny < .price) {
		mes "คุณมี Zeny ไม่เพียงพอ";
		close;
	}
	Zeny -= .price;
	// Is hack?
	if (callfunc("F_IsEquipIDHack", .@part, .@selectedItemId)) {
		mes "คุณสลับอุปกรณ์สวมใส่ ระหว่างการดำเนินการ";
		close;
	}
	// Set random option now
	.@random_option_1 = rand(1,23);
	.@random_value_1 = rand(0,#ro_max_value);
	.@random_option_2 = rand(1,23);
	.@random_value_2 = rand(0,#ro_max_value);
	.@random_option_3 = rand(1,23);
	.@random_value_3 = rand(0,#ro_max_value);
	.@random_option_4 = rand(1,23);
	.@random_value_4 = rand(0,#ro_max_value);
	.@random_option_5 = rand(1,23);
	.@random_value_5 = rand(0,#ro_max_value);
	setrandomoption(.@part,0,.@random_option_1,.@random_value_1,0);
	setrandomoption(.@part,1,.@random_option_2,.@random_value_2,0);
	setrandomoption(.@part,2,.@random_option_3,.@random_value_3,0);
	setrandomoption(.@part,3,.@random_option_4,.@random_value_4,0);
	setrandomoption(.@part,4,.@random_option_5,.@random_value_5,0);
	mes "คุณได้รับ";
	mes "๐ " + $reward_color$ + OptionDescription(.@random_option_1,.@random_value_1) + $end_color$ + " ที่ช่อง 1!";
	mes "๐ " + $reward_color$ + OptionDescription(.@random_option_2,.@random_value_2) + $end_color$ + " ที่ช่อง 2!";
	mes "๐ " + $reward_color$ + OptionDescription(.@random_option_3,.@random_value_3) + $end_color$ + " ที่ช่อง 3!";
	mes "๐ " + $reward_color$ + OptionDescription(.@random_option_4,.@random_value_4) + $end_color$ + " ที่ช่อง 4!";
	mes "๐ " + $reward_color$ + OptionDescription(.@random_option_5,.@random_value_5) + $end_color$ + " ที่ช่อง 5!";
	close;

OnInit:
	.price = 1;
	waitingroom "สุ่ม Option",0;
	end;
}
